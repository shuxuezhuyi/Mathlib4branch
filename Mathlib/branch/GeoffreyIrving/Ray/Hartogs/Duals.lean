/-
Copyright (c) 2023 Geoffrey Irving. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Geoffrey Irving
-/
import Mathlib.Analysis.NormedSpace.HahnBanach.Extension
import Mathlib.Topology.Basic
import Mathlib.branch.GeoffreyIrving.Ray.Tactic.Bound
import Mathlib.branch.GeoffreyIrving.Ray.Hartogs.MaxLog
import Mathlib.branch.GeoffreyIrving.Ray.Misc.Topology

/-!
## Norms in a separable metric space as countable sups of linear functionals

In `Subharmonic.lean`, we want to show that `log ‚Äñf z‚Äñ` is subharmonic when `f : ‚ÑÇ ‚Üí E` is
analytic, where `E` is any separable normed space.  Since countable suprema of subharmonic
functions are subharmonic, we can do this by showing `log (g (f z))` is subharmonic where
`g : E ‚Üí ‚ÑÇ` is any linear functional, then expressing `‚Äñf z‚Äñ` as a countable suprema over
linear functions.

This file chooses a countable set of linear functions `duals n : E ‚ÜíL[‚ÑÇ] ‚ÑÇ` for this purpose,
such that `‚Äñx‚Äñ = ‚®Ü n, ‚Äñduals n x‚Äñ`.
-/

open Classical
open Complex (abs exp I log)
open Filter (atTop)
open Function (curry uncurry)
open Metric (ball closedBall sphere)
open Set (range univ)
open TopologicalSpace (SecondCountableTopology)
open scoped Real NNReal ENNReal Topology ComplexConjugate

noncomputable section

variable {G : Type} [NormedAddCommGroup G]
variable {E : Type} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] [SecondCountableTopology E]

/-- A nonconstructive function which extracts a dual vector `f` exhibiting `f x = ‚Äñx‚Äñ` -/
def dualVector (x : E) : E ‚ÜíL[‚ÑÇ] ‚ÑÇ :=
  choose (exists_dual_vector'' ‚ÑÇ x)

theorem dualVector_norm (x : E) : ‚ÄñdualVector x‚Äñ ‚â§ 1 :=
  (choose_spec (exists_dual_vector'' ‚ÑÇ x)).1

theorem dualVector_nnnorm (x : E) : ‚ÄñdualVector x‚Äñ‚Çä ‚â§ 1 :=
  dualVector_norm _

@[simp]
theorem dualVector_apply (x : E) : dualVector x x = ‚Äñx‚Äñ :=
  (choose_spec (exists_dual_vector'' ‚ÑÇ x)).2

theorem dualVector_le (x y : E) : abs (dualVector x y) ‚â§ ‚Äñy‚Äñ := by
  rw [‚Üê Complex.norm_eq_abs]
  calc ‚ÄñdualVector x y‚Äñ
    _ ‚â§ ‚ÄñdualVector x‚Äñ * ‚Äñy‚Äñ := (dualVector x).le_op_norm y
    _ ‚â§ 1 * ‚Äñy‚Äñ := by bound [dualVector_norm x]
    _ = ‚Äñy‚Äñ := by simp only [one_mul]

/-- Dual vectors of a dense subset of `E` -/
def duals : ‚Ñï ‚Üí E ‚ÜíL[‚ÑÇ] ‚ÑÇ := fun n ‚Ü¶ dualVector (TopologicalSpace.denseSeq E n)

/-- Lipschitz 0 functions are constant -/
theorem LipschitzWith.is_const {g : ‚Ñù ‚Üí ‚Ñù} (g0 : LipschitzWith 0 g) : ‚àÄ x y, g x = g y := by
  intro x y; simpa only [ENNReal.coe_zero, zero_mul, nonpos_iff_eq_zero, edist_eq_zero] using g0 x y

/-- `g ‚Äñduals n x‚Äñ` is bounded above w.r.t. `n` for any monotone `g` -/
theorem duals_bddAbove {g : ‚Ñù ‚Üí ‚Ñù} (gm : Monotone g) (x : E) :
    BddAbove (range fun n ‚Ü¶ g ‚Äñduals n x‚Äñ) := by
  rw [bddAbove_def]; use g ‚Äñx‚Äñ
  simp only [Complex.norm_eq_abs, Set.mem_range, forall_exists_index,
    forall_apply_eq_imp_iff']
  intro n; apply gm; apply dualVector_le

/-- One-sided Lipschitz bounds on the reals -/
theorem LipschitzWith.le {f : G ‚Üí ‚Ñù} {k : ‚Ñù‚â•0} (fk : LipschitzWith k f) (x y : G) :
    f x ‚â§ f y + k * dist x y := by
  calc f x
    _ = f y + (f x - f y) := by ring_nf
    _ ‚â§ f y + |f x - f y| := by bound
    _ = f y + dist (f x) (f y) := by rw [Real.dist_eq]
    _ ‚â§ f y + k * dist x y := by linarith [fk.dist_le_mul x y]

/-- Norms are suprs over `duals` (version with an arbitrary monotone + Lipschitz function) -/
theorem norm_eq_duals_supr' {g : ‚Ñù ‚Üí ‚Ñù} {k : NNReal} (gm : Monotone g) (gk : LipschitzWith k g)
    (x : E) : g ‚Äñx‚Äñ = ‚®Ü n, g ‚Äñduals n x‚Äñ := by
  by_cases k0 : k = 0; ¬∑ rw [k0] at gk; have g0 := gk.is_const 0; simp only [‚Üê g0 _, ciSup_const]
  have kp : 0 < (k : ‚Ñù) := by simp only [NNReal.coe_pos]; exact Ne.bot_lt k0
  apply le_antisymm
  ¬∑ apply le_of_forall_pos_le_add; intro e ep
    rcases Metric.denseRange_iff.mp (TopologicalSpace.denseRange_denseSeq E)
      x (e / 2 / k) (by bound) with ‚ü®n, nx‚ü©
    generalize hy : TopologicalSpace.denseSeq E n = y; rw [hy] at nx
    have hn : duals n = dualVector y := by rw [‚Üê hy, duals]
    have h := le_ciSup (duals_bddAbove gm x) n
    generalize hs : ‚®Ü n, g ‚Äñduals n x‚Äñ = s
    simp_rw [hs, hn] at h; clear hs hn hy; simp only [Complex.norm_eq_abs] at h
    have gk' : LipschitzWith k fun x ‚Ü¶ g (abs (dualVector y x)) := by
      have k11 : (k : ‚Ñù‚â•0) = k * 1 * 1 := by norm_num
      rw [k11]
      simp_rw [‚ÜêComplex.norm_eq_abs]; apply (gk.comp lipschitzWith_one_norm).comp
      exact (dualVector y).lipschitz.weaken (dualVector_nnnorm y)
    calc g ‚Äñx‚Äñ
      _ ‚â§ g ‚Äñy‚Äñ + k * 1 * dist x y := (gk.comp lipschitzWith_one_norm).le x y
      _ ‚â§ g ‚Äñy‚Äñ + k * 1 * (e / 2 / k) := by bound
      _ = g ‚Äñy‚Äñ + k / k * e / 2 := by ring
      _ ‚â§ g ‚Äñy‚Äñ + 1 * e / 2 := by bound
      _ = g ‚Äñy‚Äñ + e / 2 := by simp only [one_mul]
      _ = g (abs (dualVector y y)) + e / 2 := by
        simp only [dualVector_apply, Complex.abs_ofReal, abs_norm]
      _ ‚â§ g (abs (dualVector y x)) + k * dist y x + e / 2 := by bound [gk'.le]
      _ ‚â§ s + k * dist y x + e / 2 := by linarith
      _ = s + k * dist x y + e / 2 := by rw [dist_comm]
      _ ‚â§ s + k * (e / 2 / k) + e / 2 := by bound
      _ = s + k / k * e / 2 + e / 2 := by ring_nf
      _ ‚â§ s + 1 * e / 2 + e / 2 := by bound
      _ = s + e := by ring_nf
  ¬∑ apply ciSup_le; intro n; apply gm; simp only [Complex.norm_eq_abs]; apply dualVector_le

/-- Norms are suprs over `duals` -/
theorem norm_eq_duals_iSup (x : E) : ‚Äñx‚Äñ = ‚®Ü n, ‚Äñduals n x‚Äñ := by
  have h := norm_eq_duals_supr' (@monotone_id ‚Ñù _) LipschitzWith.id x
  simpa only [Complex.norm_eq_abs] using h

/-- Norms are suprs over `duals` (`maxLog` version) -/
theorem maxLog_norm_eq_duals_iSup (b : ‚Ñù) (x : E) : maxLog b ‚Äñx‚Äñ = ‚®Ü n, maxLog b ‚Äñduals n x‚Äñ :=
  norm_eq_duals_supr' (monotone_maxLog b) (LipschitzWith.maxLog b) x

/-- Rewrite a `‚Ñï` supr into a monotonic limit -/
theorem Csupr.has_lim (s : ‚Ñï ‚Üí ‚Ñù) (ba : BddAbove (range s)) :
    Filter.Tendsto (fun n ‚Ü¶ partialSups s n) atTop (ùìù (‚®Ü n, s n)) := by
  rw [Metric.tendsto_atTop]; intro e ep
  generalize hb : (‚®Ü n, s n) - e = b
  have bs : b < ‚®Ü n, s n := by rw [‚Üê hb]; exact sub_lt_self _ (by linarith)
  rcases exists_lt_of_lt_ciSup bs with ‚ü®N, sN‚ü©
  use N; intro n nN; rw [Real.dist_eq]; rw [abs_lt]; constructor
  ¬∑ simp only [neg_lt_sub_iff_lt_add]; simp only [‚Üêhb] at sN
    calc iSup s
      _ = iSup s - e + e := by ring
      _ < s N + e := by linarith
      _ ‚â§ partialSups s n + e := by linarith [le_partialSups_of_le s nN]
      _ = e + partialSups s n := by ring
  ¬∑ have rs : partialSups s n ‚â§ iSup s := partialSups_le _ _ _ fun a _ ‚Ü¶ le_ciSup ba a
    calc partialSups s n - iSup s
      _ ‚â§ iSup s - iSup s := by linarith
      _ = 0 := by ring
      _ < e := ep

/-- Partial sups of `maxLog b ‚Äñduals k x‚Äñ` converge to `maxLog b ‚Äñx‚Äñ` -/
theorem duals_lim_tendsto_maxLog_norm (b : ‚Ñù) (x : E) :
    Filter.Tendsto (partialSups fun k ‚Ü¶ maxLog b ‚Äñduals k x‚Äñ) atTop (ùìù (maxLog b ‚Äñx‚Äñ)) := by
  rw [maxLog_norm_eq_duals_iSup]; exact Csupr.has_lim _ (duals_bddAbove (monotone_maxLog _) _)

/-- Partial sups of `maxLog b ‚Äñduals k x‚Äñ` converge to `maxLog b ‚Äñx‚Äñ` -/
theorem maxLog_norm_eq_duals_limUnder (b : ‚Ñù) (x : E) :
    maxLog b ‚Äñx‚Äñ = limUnder atTop (partialSups fun k ‚Ü¶ maxLog b ‚Äñduals k x‚Äñ) :=
  haveI a := duals_lim_tendsto_maxLog_norm b x
  tendsto_nhds_unique a (tendsto_nhds_limUnder ‚ü®_, a‚ü©)
