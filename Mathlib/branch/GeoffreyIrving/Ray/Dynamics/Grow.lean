/-
Copyright (c) 2023 Geoffrey Irving. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Geoffrey Irving
-/
-- Bottcher map throughout a superattracting basis (up to critical points)
import Mathlib.Topology.ExtendFrom
import Mathlib.Topology.Compactness.Compact
import Mathlib.branch.GeoffreyIrving.Ray.AnalyticManifold.LocalInj
import Mathlib.branch.GeoffreyIrving.Ray.AnalyticManifold.Nonseparating
import Mathlib.branch.GeoffreyIrving.Ray.Dynamics.Postcritical
import Mathlib.branch.GeoffreyIrving.Ray.Dynamics.Potential
import Mathlib.branch.GeoffreyIrving.Ray.Misc.Connected
import Mathlib.branch.GeoffreyIrving.Ray.Misc.Continuation

/-!
## Analytic continuation of external rays for all postcritical values

After `BottcherNearM.lean` and `Potential.lean`, we have B√∂ttcher coordinates
`s.bottcherNear : ‚ÑÇ ‚Üí S ‚Üí ‚ÑÇ` defined near `a`, and `s.potential : ‚ÑÇ ‚Üí S ‚Üí ‚Ñù` continuous everywhere
if `OnePreimage s`.  `s.bottcherNear` is invertible at any `(c,a)`, so near `a` we have an external
ray map `ray : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S`.

We now grow these rays out to the critical potential `s.p c`, which will give a ray map analytic
throughout `s.post`.  We fix `c`, require `ray` to be analytic on a neighborhood of
`{c} √óÀ¢ closedBall 0 p`, and apply continuous induction to increase `p` from a small value up to
(right below) `s.p c`.  The resulting map is unique near any `c`, so we can stitch the continuations
for all `c` together into a single map (`Super.has_ray`).

A lot of the detail here is related to working with analytic functions in neighborhoods of points
and sets without using the heavier machinery of germs, stalks, and sheaves.  But I don't know that
machinery well, so I'm sticking to the low tech approach for now.

The defining equation of external rays `r`, with `c` suppressed, is
  `bottcher (r x) = x`
However, we know `bottcher` only locally near `a`, specifically on `s.near`.  If we have `n` s.t.
`f^[n] z ‚àà s.near`, we can map the above equation forward `n` times to get
  `bottcher (f^[n] (r x)) = bottcher (r x) ^ d ^ n = x ^ d ^ n`
-/

-- Remove once https://github.com/leanprover/lean4/issues/2220 is fixed
local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y)

open Classical
open Complex (abs)
open Filter (Tendsto atTop eventually_of_forall)
open Function (curry uncurry)
open Metric (ball closedBall isOpen_ball ball_mem_nhds mem_ball mem_closedBall mem_ball_self)
open OneDimension
open Set
open scoped Topology
noncomputable section

-- All information for a monic superattracting fixed point at the origin
variable {S : Type} [TopologicalSpace S] [CompactSpace S] [T3Space S] [ChartedSpace ‚ÑÇ S]
  [AnalyticManifold I S]
variable {f : ‚ÑÇ ‚Üí S ‚Üí S}
variable {c : ‚ÑÇ}
variable {a z : S}
variable {d n : ‚Ñï}
variable {p : ‚Ñù}
variable {s : Super f d a}
variable {r : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S}

/-- `Eqn s n r (c,z)` means `r` looks locally like external rays near `z`, mapping forwards
    by `f c^[n]` to hit `s.near`. -/
structure Eqn (s : Super f d a) (n : ‚Ñï) (r : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S) (x : ‚ÑÇ √ó ‚ÑÇ) : Prop where
  holo : HolomorphicAt II I (uncurry r) x
  near : (x.1, (f x.1)^[n] (r x.1 x.2)) ‚àà s.near
  eqn : s.bottcherNear x.1 ((f x.1)^[n] (r x.1 x.2)) = x.2 ^ d ^ n

/-- `r` is an external ray map in a neighborhood of `{c} √óÀ¢ closedBall 0 p` -/
structure Grow (s : Super f d a) (c : ‚ÑÇ) (p : ‚Ñù) (n : ‚Ñï) (r : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S) : Prop where
  nonneg : 0 ‚â§ p
  zero : r c 0 = a
  start : ‚àÄ·∂† x : ‚ÑÇ √ó ‚ÑÇ in ùìù (c, 0), s.bottcherNear x.1 (r x.1 x.2) = x.2
  eqn : ‚àÄ·∂† x : ‚ÑÇ √ó ‚ÑÇ in ùìùÀ¢ ({c} √óÀ¢ closedBall 0 p), Eqn s n r x

/-- Construct `Eqn` using fewer `‚àÄ·∂†` -/
theorem eqn_near {s : Super f d a} {n : ‚Ñï} {r : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S} {c x : ‚ÑÇ}
    (holo : HolomorphicAt II I (uncurry r) (c, x)) (mem : (c, (f c)^[n] (r c x)) ‚àà s.near)
    (loc : ‚àÄ·∂† y : ‚ÑÇ √ó ‚ÑÇ in ùìù (c, x), s.bottcherNear y.1 ((f y.1)^[n] (r y.1 y.2)) = y.2 ^ d ^ n) :
    ‚àÄ·∂† y in ùìù (c, x), Eqn s n r y := by
  have m : ‚àÄ·∂† y : ‚ÑÇ √ó ‚ÑÇ in ùìù (c, x), (y.1, (f y.1)^[n] (r y.1 y.2)) ‚àà s.near := by
    refine' ContinuousAt.eventually_mem _ s.isOpen_near mem
    exact continuousAt_fst.prod (s.continuousAt_iter continuousAt_fst holo.continuousAt)
  apply holo.eventually.mp; apply loc.mp; apply m.mp
  apply eventually_of_forall; intro _ m l h; exact ‚ü®h, m, l‚ü©

/-- `Eqn` is local -/
theorem Eqn.congr {x : ‚ÑÇ √ó ‚ÑÇ} {r0 r1 : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S} (e : Eqn s n r0 x)
    (loc : uncurry r0 =·∂†[ùìù x] uncurry r1) : Eqn s n r1 x := by
  have s := loc.self; simp only [uncurry] at s
  exact
    { holo := e.holo.congr loc
      near := by simp only [‚Üê s, e.near]
      eqn := by simp only [‚Üê s, e.eqn] }

/-- We can increase `n` in `Eqn` -/
theorem Eqn.mono {x : ‚ÑÇ √ó ‚ÑÇ} (e : Eqn s n r x) {m : ‚Ñï} (nm : n ‚â§ m) : Eqn s m r x :=
  { holo := e.holo
    near := s.iter_stays_near' e.near nm
    eqn := by
      refine' Nat.le_induction e.eqn _ m nm; intro k nk h
      simp only [h, Function.iterate_succ_apply',
        s.bottcherNear_eqn (s.iter_stays_near' e.near nk), pow_succ', pow_mul] }

/-- We can increase `n` in `Grow` -/
theorem Grow.mono (g : Grow s c p n r) {m : ‚Ñï} (nm : n ‚â§ m) : Grow s c p m r :=
  { nonneg := g.nonneg
    zero := g.zero
    start := g.start
    eqn := g.eqn.mp (eventually_of_forall fun _ e ‚Ü¶ e.mono nm) }

/-- Centers `(c,0)` are in the domain -/
theorem mem_domain (c : ‚ÑÇ) {p : ‚Ñù} (p0 : 0 ‚â§ p) :
    (c, (0 : ‚ÑÇ)) ‚àà ({c} √óÀ¢ closedBall 0 p : Set (‚ÑÇ √ó ‚ÑÇ)) :=
  mk_mem_prod rfl (Metric.mem_closedBall_self p0)

/-- The boundary is in the domain -/
theorem mem_domain_self {c x : ‚ÑÇ} :
    (c, x) ‚àà ({c} √óÀ¢ closedBall 0 (Complex.abs x) : Set (‚ÑÇ √ó ‚ÑÇ)) := by
  simp only [mem_prod_eq, mem_singleton_iff, eq_self_iff_true, mem_closedBall, Complex.dist_eq,
    sub_zero, true_and_iff, le_refl]

/-- Our domain is preconnected -/
theorem domain_preconnected (c : ‚ÑÇ) (p : ‚Ñù) :
    IsPreconnected ({c} √óÀ¢ closedBall 0 p : Set (‚ÑÇ √ó ‚ÑÇ)) :=
  isPreconnected_singleton.prod (convex_closedBall _ _).isPreconnected

/-- Our domain is monotonic in `p` -/
theorem domain_mono (c : ‚ÑÇ) {p0 p1 : ‚Ñù} (le : p0 ‚â§ p1) :
    ({c} √óÀ¢ closedBall 0 p0 : Set (‚ÑÇ √ó ‚ÑÇ)) ‚äÜ {c} √óÀ¢ closedBall 0 p1 :=
  prod_mono_right (Metric.closedBall_subset_closedBall le)

/-- If `closedBall 0 p ‚äÜ t`, we can increase `p` bit without leaving `t` -/
theorem domain_open' {p : ‚Ñù} {t : Set ‚ÑÇ} (sub : closedBall (0 : ‚ÑÇ) p ‚äÜ t) (ot : IsOpen t) :
    ‚àÉ q, p < q ‚àß closedBall 0 q ‚äÜ t := by
  set u := Complex.abs '' (closedBall 0 (p + 1) \ t)
  by_cases ne : u = ‚àÖ
  ¬∑ refine ‚ü®p + 1, by bound, ?_‚ü©; rw [image_eq_empty, diff_eq_empty] at ne; exact ne
  replace ne := nonempty_iff_ne_empty.mpr ne
  have uc : IsClosed u :=
    (((isCompact_closedBall _ _).diff ot).image Complex.continuous_abs).isClosed
  have up : ‚àÄ x : ‚Ñù, x ‚àà u ‚Üí p < x := by
    intro x m; rcases m with ‚ü®z, ‚ü®_, mt‚ü©, e‚ü©; rw [‚Üê e]; contrapose mt
    simp only [not_not, not_lt] at mt ‚ä¢
    apply sub; simp only [mem_closedBall, Complex.dist_eq, sub_zero, mt]
  have ub : BddBelow u := ‚ü®p, fun _ m ‚Ü¶ (up _ m).le‚ü©
  have iu : sInf u ‚àà u := IsClosed.csInf_mem uc ne ub
  rcases exists_between (up _ iu) with ‚ü®q, pq, qi‚ü©
  use min q (p + 1), lt_min pq (by linarith)
  intro z m; simp only [mem_closedBall, Complex.dist_eq, sub_zero, le_min_iff] at m
  rcases m with ‚ü®zq, zp‚ü©; have zi := lt_of_le_of_lt zq qi
  contrapose zi; simp only [not_lt]; refine' csInf_le ub (mem_image_of_mem _ _)
  simp only [mem_diff, mem_closedBall, Complex.dist_eq, sub_zero]; use zp, zi

/-- If `{c} √óÀ¢ closedBall 0 p ‚äÜ t`, we can increase `p` bit without leaving `t` -/
theorem domain_open {p : ‚Ñù} {t : Set (‚ÑÇ √ó ‚ÑÇ)} (sub : {c} √óÀ¢ closedBall 0 p ‚äÜ t) (o : IsOpen t) :
    ‚àÉ q, p < q ‚àß {c} √óÀ¢ closedBall 0 q ‚äÜ t := by
  have sub : closedBall 0 p ‚äÜ {b | (c, b) ‚àà t} := by
    intro z m; simp only [mem_setOf]; apply sub; exact ‚ü®mem_singleton _, m‚ü©
  rcases domain_open' sub (o.snd_preimage c) with ‚ü®q, pq, sub‚ü©
  use q, pq; intro ‚ü®e, z‚ü© ‚ü®ec, m‚ü©; simp only [mem_singleton_iff] at ec
  replace m := sub m; simp only [‚Üê ec, mem_setOf] at m; exact m

/-- `Grow` is local -/
theorem Grow.congr {r0 r1 : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S} (g : Grow s c p n r0)
    (e : uncurry r0 =·∂†[ùìùÀ¢ ({c} √óÀ¢ closedBall 0 p)] uncurry r1) : Grow s c p n r1 :=
  { nonneg := g.nonneg
    zero := by
      have e := e.self_set _ (mem_domain c g.nonneg)
      simp only [uncurry] at e; rw [‚Üê e]; exact g.zero
    start := by
      refine' g.start.mp ((e.filter_mono (nhds_le_nhdsSet (mem_domain c g.nonneg))).mp _)
      refine' eventually_of_forall fun x e s ‚Ü¶ _
      simp only [uncurry] at e; rw [‚Üê e]; exact s
    eqn := by
      have eqn := g.eqn; simp only [Filter.EventuallyEq, eventually_nhdsSet_iff_forall] at eqn e ‚ä¢
      intro x m
      refine' (eqn x m).mp ((e x m).eventually_nhds.mp (eventually_of_forall fun y e eqn ‚Ü¶ _))
      exact eqn.congr e }

/-- `s.potential (r x) = abs x`, if `Eqn s n r x` -/
theorem Eqn.potential {x : ‚ÑÇ √ó ‚ÑÇ} (e : Eqn s n r x) : s.potential x.1 (r x.1 x.2) = abs x.2 := by
  simp only [s.potential_eq e.near, Super.potential', e.eqn, Complex.abs.map_pow, ‚Üê Nat.cast_pow,
    Real.pow_nat_rpow_nat_inv (Complex.abs.nonneg _) (pow_ne_zero _ s.d0)]

/-- `Eqn` implies that `s.bottcherNearIter` is noncritical -/
theorem eqn_noncritical {x : ‚ÑÇ √ó ‚ÑÇ} (e : ‚àÄ·∂† y in ùìù x, Eqn s n r y) (x0 : x.2 ‚â† 0) :
    mfderiv I I (s.bottcherNearIter n x.1) (r x.1 x.2) ‚â† 0 := by
  rcases x with ‚ü®c, x‚ü©; contrapose x0; simp only [not_not] at x0 ‚ä¢
  replace x0 : mfderiv I I (fun y ‚Ü¶ s.bottcherNearIter n c (r c y)) x = 0 := by
    rw [‚ÜêFunction.comp_def,
      mfderiv_comp x (s.bottcherNearIter_holomorphic e.self.near).in2.mdifferentiableAt
        e.self.holo.in2.mdifferentiableAt,
      x0, ContinuousLinearMap.zero_comp]
  have loc : (fun y ‚Ü¶ s.bottcherNearIter n c (r c y)) =·∂†[ùìù x] fun y ‚Ü¶ y ^ d ^ n :=
    ((continuousAt_const.prod continuousAt_id).eventually e).mp
      (eventually_of_forall fun _ e ‚Ü¶ e.eqn)
  rw [mfderiv_eq_fderiv, loc.fderiv_eq] at x0
  have d := (differentiableAt_pow (ùïú := ‚ÑÇ) (x := x) (d ^ n)).hasFDerivAt.hasDerivAt.deriv
  rw [x0, ContinuousLinearMap.zero_apply, deriv_pow, mul_eq_zero, Nat.cast_eq_zero,
    pow_eq_zero_iff', pow_eq_zero_iff'] at d
  simp only [s.d0, false_and_iff, false_or_iff] at d; exact d.1

/-- `p < 1` for any `p` in `Grow` -/
theorem Grow.p1 (g : Grow s c p n r) : p < 1 := by
  by_contra p1; simp only [not_lt] at p1
  have e := (g.eqn.filter_mono (nhds_le_nhdsSet (x := (c, 1)) ?_)).self
  ¬∑ have lt := s.potential_lt_one ‚ü®_, e.near‚ü©
    rw [e.potential, Complex.abs.map_one, lt_self_iff_false] at lt
    exact lt
  ¬∑ simp only [p1, singleton_prod, mem_image, mem_closedBall_zero_iff, Complex.norm_eq_abs,
      Prod.mk.inj_iff, eq_self_iff_true, true_and_iff, exists_eq_right, Complex.abs.map_one]

/-- `r` is analytic throughout the domain -/
theorem Grow.holo (g : Grow s c p n r) : HolomorphicOn II I (uncurry r) ({c} √óÀ¢ closedBall 0 p) :=
  fun _ m ‚Ü¶ (g.eqn.filter_mono (nhds_le_nhdsSet m)).self.holo

/-- `Grow` exists for small `p`, since small `p` is near `a` -/
theorem Super.grow_start (s : Super f d a) (c : ‚ÑÇ) : ‚àÉ p r, 0 < p ‚àß Grow s c p 0 r := by
  have ba := s.bottcherNear_holomorphic _ (s.mem_near c)
  have nc := s.bottcherNear_mfderiv_ne_zero c
  rcases complex_inverse_fun ba nc with ‚ü®r, ra, rb, br‚ü©
  rw [s.bottcherNear_a] at ra br
  have rm : ‚àÄ·∂† x : ‚ÑÇ √ó ‚ÑÇ in ùìù (c, 0), (x.1, r x.1 x.2) ‚àà s.near := by
    apply (continuousAt_fst.prod ra.continuousAt).eventually_mem s.isOpen_near
    have r0 := rb.self; simp only [s.bottcherNear_a] at r0
    simp only [uncurry, r0]; exact s.mem_near c
  rcases eventually_nhds_iff.mp (ra.eventually.and (br.and rm)) with ‚ü®t, h, o, m‚ü©
  rcases Metric.isOpen_iff.mp o _ m with ‚ü®p, pp, sub‚ü©
  replace h := fun (x : ‚ÑÇ √ó ‚ÑÇ) m ‚Ü¶ h x (sub m)
  have nb : ball (c, (0 : ‚ÑÇ)) p ‚àà ùìùÀ¢ ({c} √óÀ¢ closedBall (0 : ‚ÑÇ) (p / 2)) := by
    rw [isOpen_ball.mem_nhdsSet, ‚Üê ball_prod_same]; apply prod_mono
    rw [singleton_subset_iff]; exact mem_ball_self pp
    apply Metric.closedBall_subset_ball; exact half_lt_self pp
  use p / 2, r, half_pos pp
  exact
    { nonneg := (half_pos pp).le
      zero := by convert rb.self; simp only [s.bottcherNear_a]
      start := Filter.eventually_iff_exists_mem.mpr ‚ü®_, ball_mem_nhds _ pp, fun _ m ‚Ü¶ (h _ m).2.1‚ü©
      eqn :=
        Filter.eventually_iff_exists_mem.mpr
          ‚ü®_, nb, fun _ m ‚Ü¶
            { holo := (h _ m).1
              near := (h _ m).2.2
              eqn := by simp only [Function.iterate_zero_apply, pow_zero, pow_one, (h _ m).2.1] }‚ü© }

/-- We can grow `p` and vary `c` a bit in `Grow` -/
theorem Grow.open (g : Grow s c p n r) : ‚àÉ p', p < p' ‚àß ‚àÄ·∂† c' in ùìù c, Grow s c' p' n r := by
  have e := g.eqn; simp only [nhdsSet_prod isCompact_singleton (isCompact_closedBall _ _)] at e
  rcases Filter.mem_prod_iff.mp e with ‚ü®a', an, b', bn, sub‚ü©
  simp only [subset_setOf] at sub
  rcases eventually_nhds_iff.mp (nhdsSet_singleton.subst an) with ‚ü®a, aa, ao, am‚ü©
  rcases eventually_nhdsSet_iff.mp bn with ‚ü®b, bo, bp, bb‚ü©
  rcases domain_open' bp bo with ‚ü®q, pq, qb‚ü©
  use q, pq
  have m : ‚àÄ·∂† c' in ùìù c, (c', r c' 0) ‚àà s.near := by
    refine' (continuousAt_id.prod _).eventually_mem s.isOpen_near _
    exact (g.eqn.filter_mono (nhds_le_nhdsSet (mem_domain c g.nonneg))).self.holo.in1.continuousAt
    simp only [id, g.zero, s.mem_near c]
  apply m.mp
  apply ((continuousAt_id.prod continuousAt_const).eventually g.start.eventually_nhds).mp
  refine' eventually_nhds_iff.mpr ‚ü®a, _, ao, am‚ü©
  intro c' am' start m
  exact
    { nonneg := _root_.trans g.nonneg pq.le
      zero := by have e := start.self; simp only [id, s.bottcherNear_eq_zero m] at e; exact e
      start
      eqn := by
        refine' eventually_nhdsSet_iff.mpr ‚ü®a √óÀ¢ b, ao.prod bo, _, _‚ü©
        exact prod_mono (singleton_subset_iff.mpr am') qb
        intro x ‚ü®cm, xm‚ü©; exact sub x ‚ü®aa _ cm, bb _ xm‚ü© }

/-- We can decrease `p` in `Grow` -/
theorem Grow.anti (g : Grow s c p n r) {q : ‚Ñù} (nonneg : 0 ‚â§ q) (le : q ‚â§ p) : Grow s c q n r :=
  { nonneg
    zero := g.zero
    start := g.start
    eqn :=
      g.eqn.filter_mono (nhdsSet_mono (prod_mono_right (Metric.closedBall_subset_closedBall le))) }

/-- The equivalent of `Grow` on `{c} √óÀ¢ ball 0 p`, where the ball is open rather than closed.
    However, we use an `n` that covers the boundary at potential `p` as well, so that analytic
    continuation will work without changing `n`. -/
structure GrowOpen (s : Super f d a) (c : ‚ÑÇ) (p : ‚Ñù) (r : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S) : Prop where
  pos : 0 < p
  post : p < s.p c
  zero : r c 0 = a
  start : ‚àÄ·∂† x : ‚ÑÇ √ó ‚ÑÇ in ùìù (c, 0), s.bottcherNear x.1 (r x.1 x.2) = x.2
  eqn : ‚àÄ·∂† x : ‚ÑÇ √ó ‚ÑÇ in ùìùÀ¢ ({c} √óÀ¢ ball 0 p), Eqn s (s.np c p) r x

/-- We can analytically continue `r` to any point in the closure -/
theorem GrowOpen.point (g : GrowOpen s c p r) [OnePreimage s] {x : ‚ÑÇ} (ax : abs x ‚â§ p) :
    ‚àÉ r' : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S,
      (‚àÄ·∂† y : ‚ÑÇ √ó ‚ÑÇ in ùìù (c, x), Eqn s (s.np c p) r' y) ‚àß
        ‚àÉ·∂† y in ùìù x, y ‚àà ball (0 : ‚ÑÇ) p ‚àß r' c y = r c y := by
  -- If z = a, we can use r
  by_cases za : abs x = 0
  ¬∑ use r; simp only [Complex.abs.eq_zero] at za; simp only [za, eq_self_iff_true, and_true_iff]
    constructor
    refine' g.eqn.filter_mono (nhds_le_nhdsSet _); exact mk_mem_prod rfl (mem_ball_self g.pos)
    exact (isOpen_ball.eventually_mem (mem_ball_self g.pos)).frequently
  replace za := (Ne.symm za).lt_of_le (Complex.abs.nonneg _)
  -- Choose a value z = r' c x as a cluster point of r c at ùìù[t] x
  set t := ball (0 : ‚ÑÇ) p
  have xt : x ‚àà closure t := by
    simp only [closure_ball _ g.pos.ne', mem_closedBall, Complex.dist_eq, sub_zero, ax]
  have ez : ‚àÉ z : S, MapClusterPt z (ùìù[t] x) (r c) :=
    @cluster_point_of_compact _ _ _ _
      (Filter.map_neBot (hf := mem_closure_iff_nhdsWithin_neBot.mp xt))
  rcases ez with ‚ü®z, cp‚ü©
  have pz : s.potential c z = abs x := by
    refine'
      eq_of_nhds_neBot (cp.map (Continuous.potential s).in2.continuousAt (Filter.tendsto_map' _))
    have e : ‚àÄ y, y ‚àà t ‚Üí (s.potential c ‚àò r c) y = abs y := by
      intro y m; simp only [Function.comp]; exact (g.eqn.self_set (c, y) ‚ü®rfl, m‚ü©).potential
    exact tendsto_nhdsWithin_congr (fun t m ‚Ü¶ (e t m).symm)
      Complex.continuous_abs.continuousWithinAt
  rcases s.nice_np c (lt_of_lt_of_le g.post s.p_le_one) z (_root_.trans (le_of_eq pz) ax)
    with ‚ü®m, nc‚ü©
  replace nc := nc _ (le_refl _)
  generalize hn : s.np c p = n; rw [hn] at m nc
  generalize hb : s.bottcherNearIter n = b
  have bz : b c z = x ^ d ^ n := by
    refine' eq_of_nhds_neBot (cp.map _ (Filter.tendsto_map' _))
    rw [‚Üê hb]; exact (s.bottcherNearIter_holomorphic m).in2.continuousAt
    have e : ‚àÄ y, y ‚àà t ‚Üí (b c ‚àò r c) y = y ^ d ^ n := by
      intro y m; simp only [Function.comp, ‚Üê hb, ‚Üê hn]; exact (g.eqn.self_set (c, y) ‚ü®rfl, m‚ü©).eqn
    exact tendsto_nhdsWithin_congr (fun t m ‚Ü¶ (e t m).symm) (continuous_pow _).continuousWithinAt
  have post : Postcritical s c z := lt_of_le_of_lt (_root_.trans (le_of_eq pz) ax) g.post
  rw [‚Üê pz] at za
  -- Invert s.bottcherNearIter at z
  have ba := s.bottcherNearIter_holomorphic m
  replace nc := s.bottcherNearIter_mfderiv_ne_zero nc (post.not_precritical za.ne')
  rcases complex_inverse_fun ba nc with ‚ü®i, ia, ib, bi‚ü©
  simp only [hb, bz] at ia bi ib
  have pt : Tendsto (fun p : ‚ÑÇ √ó ‚ÑÇ ‚Ü¶ (p.1, p.2 ^ d ^ n)) (ùìù (c, x)) (ùìù (c, x ^ d ^ n)) :=
    continuousAt_fst.prod (continuousAt_snd.pow _)
  have ian : HolomorphicAt II I (uncurry fun e y : ‚ÑÇ ‚Ü¶ i e (y ^ d ^ n)) (c, x) :=
    ia.curry_comp_of_eq holomorphicAt_fst holomorphicAt_snd.pow rfl
  use fun e y ‚Ü¶ i e (y ^ d ^ n); constructor
  ¬∑ -- We satisfy eqn near x
    apply eqn_near ian
    ¬∑ simp only [‚Üêbz]; rw [ib.self]; exact m
    ¬∑ refine' (pt.eventually bi).mp (eventually_of_forall _)
      intro _ bi; simp only [‚Üê hb] at bi; exact bi
  ¬∑ -- We frequently match r, by local injectivity of b
    have ne : MapClusterPt (z, z) (ùìù[t] x) fun y ‚Ü¶ (r c y, i c (y ^ d ^ n)) := by
      apply cp.prod; refine' Filter.Tendsto.mono_left _ nhdsWithin_le_nhds
      have ic := ian.in2.continuousAt
      simp only [ContinuousAt, ‚Üêbz] at ic; rw [ib.self] at ic
      exact ic
    have inj := (@Filter.Eventually.frequently _ _ ne _
            (Filter.Eventually.filter_mono inf_le_left (ba.in2.local_inj nc))).filter_mono
        inf_le_right
    simp only [Filter.frequently_map, frequently_nhdsWithin_iff] at inj
    apply inj.mp
    apply ((continuousAt_const.prod (continuousAt_pow _ _)).eventually bi).mp
    apply eventually_of_forall; simp only [‚Üê hb, ‚Üê hn]; intro x bi ‚ü®inj, m‚ü©
    refine' ‚ü®m, (inj _).symm‚ü©; simp only [bi]
    exact (g.eqn.self_set ‚ü®c, x‚ü© (mk_mem_prod rfl m)).eqn

/-- `Eqn` determines `r` locally, given equality at a point -/
theorem eqn_unique {r0 r1 : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S} {x : ‚ÑÇ √ó ‚ÑÇ} (e0 : ‚àÄ·∂† y in ùìù x, Eqn s n r0 y)
    (e1 : ‚àÄ·∂† y in ùìù x, Eqn s n r1 y) (r01 : r0 x.1 x.2 = r1 x.1 x.2) (x0 : x.2 ‚â† 0) :
    uncurry r0 =·∂†[ùìù x] uncurry r1 := by
  have ba := s.bottcherNearIter_holomorphic e0.self.near
  have inj := ba.local_inj' (eqn_noncritical e0 x0); nth_rw 2 [r01] at inj
  have t : Tendsto (fun x : ‚ÑÇ √ó ‚ÑÇ ‚Ü¶ (x.1, r0 x.1 x.2, r1 x.1 x.2)) (ùìù x)
      (ùìù (x.1, r0 x.1 x.2, r1 x.1 x.2)) :=
    continuousAt_fst.prod (e0.self.holo.continuousAt.prod e1.self.holo.continuousAt)
  apply (t.eventually inj).mp
  refine' e0.mp (e1.mp (eventually_of_forall fun x e1 e0 inj ‚Ü¶ _))
  specialize inj _
  simp only [Prod.fst]
  simp only [uncurry, Prod.fst, Prod.snd, Super.bottcherNearIter, e0.eqn, e1.eqn]
  simp only [uncurry, Prod.fst, Prod.snd, Prod.ext_iff] at inj; exact inj

/-- The property that we will use to define valid germs in analytic continuation.
    This is normally just `Eqn`, but requiring `=·∂†[ùìù (c,0)]` if we're at the origin
    since there `Eqn` uniqueness breaks down. -/
structure Eqns (s : Super f d a) (n : ‚Ñï) (r0 r : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S) (x : ‚ÑÇ √ó ‚ÑÇ) : Prop where
  eqn : ‚àÄ·∂† y in ùìù x, Eqn s n r y
  start : x.2 = 0 ‚Üí uncurry r =·∂†[ùìù x] uncurry r0

/-- `Eqns` implies `r` is analytic -/
theorem Eqns.holo {r0 r : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S} {x : ‚ÑÇ √ó ‚ÑÇ} (e : Eqns s n r0 r x) :
    HolomorphicAt II I (uncurry r) x :=
  e.eqn.self.holo

/-- `Eqns` is local -/
theorem Eqns.congr {x : ‚ÑÇ √ó ‚ÑÇ} {r0 r1 r2 : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S} (e1 : Eqns s n r0 r1 x)
    (loc : uncurry r1 =·∂†[ùìù x] uncurry r2) : Eqns s n r0 r2 x :=
  { eqn := e1.eqn.mp (loc.eventually_nhds.mp (eventually_of_forall fun _ loc e ‚Ü¶ e.congr loc))
    start := fun x0 ‚Ü¶ loc.symm.trans (e1.start x0) }

/-- `Eqns` determines `r` once one point is fixed -/
theorem eqns_unique {r0 r1 r2 : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S} {t : Set (‚ÑÇ √ó ‚ÑÇ)}
    (pre : IsPreconnected t) (e1 : ‚àÄ x, x ‚àà t ‚Üí Eqns s n r0 r1 x)
    (e2 : ‚àÄ x, x ‚àà t ‚Üí Eqns s n r0 r2 x) (ne : ‚àÉ x, x ‚àà t ‚àß uncurry r1 x = uncurry r2 x) :
    EqOn (uncurry r1) (uncurry r2) t := by
  -- The set on which r0 = r1 is both relatively open and closed, so it's everything
  set u := {x | uncurry r1 x = uncurry r2 x}
  replace ne : (t ‚à© u).Nonempty := ne
  have op : t ‚à© u ‚äÜ interior u := by
    intro ‚ü®c, x‚ü© ‚ü®mt, mu‚ü©; rw [mem_interior_iff_mem_nhds]
    by_cases x0 : x = 0; exact ((e1 _ mt).start x0).trans ((e2 _ mt).start x0).symm
    exact eqn_unique (e1 _ mt).eqn (e2 _ mt).eqn mu x0
  have cl : t ‚à© closure u ‚äÜ u := by
    intro x ‚ü®mt, mu‚ü©; simp only [mem_setOf, mem_closure_iff_frequently, mem_inter_iff] at mu ‚ä¢
    exact tendsto_nhds_unique_of_frequently_eq (e1 _ mt).holo.continuousAt
      (e2 _ mt).holo.continuousAt mu
  exact _root_.trans (pre.relative_clopen ne op cl) interior_subset

/-- `r` is unique in `Grow` -/
theorem Grow.unique {r0 r1 : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S} {p0 p1 : ‚Ñù} {n0 n1 : ‚Ñï} (g0 : Grow s c p0 n0 r0)
    (g1 : Grow s c p1 n1 r1) (p01 : p0 ‚â§ p1) :
    uncurry r0 =·∂†[ùìùÀ¢ ({c} √óÀ¢ closedBall 0 p0)] uncurry r1 := by
  -- Reduce to equality near (c,0)
  by_cases pos : p0 < 0
  ¬∑ simp only [Metric.closedBall_eq_empty.mpr pos, singleton_prod, image_empty, nhdsSet_empty,
      Filter.EventuallyEq, Filter.eventually_bot]
  have m : (c, (0 : ‚ÑÇ)) ‚àà {c} √óÀ¢ closedBall (0 : ‚ÑÇ) p0 := mem_domain c (not_lt.mp pos)
  refine' HolomorphicOn.eq_of_locally_eq g0.holo (g1.holo.mono (domain_mono _ p01))
      (domain_preconnected _ _) ‚ü®(c, 0), m, _‚ü©
  -- Injectivity of s.bottcherNear gives us the rest
  have t : ContinuousAt (fun x : ‚ÑÇ √ó ‚ÑÇ ‚Ü¶ (x.1, r0 x.1 x.2, r1 x.1 x.2)) (c, 0) :=
    continuousAt_fst.prod
      ((g0.eqn.filter_mono (nhds_le_nhdsSet m)).self.holo.continuousAt.prod
        (g1.eqn.filter_mono (nhds_le_nhdsSet (domain_mono c p01 m))).self.holo.continuousAt)
  simp only [ContinuousAt, g0.zero, g1.zero] at t
  have inj := (s.bottcherNear_holomorphic _ (s.mem_near c)).local_inj'
    (s.bottcherNear_mfderiv_ne_zero c)
  refine' ((t.eventually inj).and (g0.start.and g1.start)).mp (eventually_of_forall _)
  intro ‚ü®e, y‚ü© ‚ü®inj, s0, s1‚ü©; exact inj (s0.trans s1.symm)

/-- Given `GrowOpen _ _ p`, we can analytically continue to the boundary to get `Grow _ _ p` -/
theorem GrowOpen.grow (g : GrowOpen s c p r) [OnePreimage s] : ‚àÉ r', Grow s c p (s.np c p) r' := by
  set n := s.np c p
  have b : Base (fun f x ‚Ü¶ Eqns s n r (curry f) x) ({c} √óÀ¢ ball (0 : ‚ÑÇ) p) (uncurry r) :=
    { convex := (convex_singleton c).prod (convex_ball 0 p)
      compact := by
        simp only [closure_prod_eq, closure_ball _ g.pos.ne', closure_singleton]
        exact isCompact_singleton.prod (isCompact_closedBall _ _)
      congr := by intro r0 r1 x e0 r01; exact e0.congr (by simp only [Function.uncurry_curry, r01])
      start := by
        simp only [Filter.eventually_iff]; rw [mem_nhdsSet_iff_forall]; intro x m
        exact (g.eqn.filter_mono (nhds_le_nhdsSet m)).eventually_nhds.mp
          (eventually_of_forall fun y e ‚Ü¶
          { eqn := e
            start := by
              simp only [Function.curry_uncurry, Filter.EventuallyEq.refl, imp_true_iff] })
      point := by
        intro ‚ü®c', x‚ü© m
        simp only [closure_prod_eq, closure_ball _ g.pos.ne', closure_singleton, mem_prod_eq,
          mem_singleton_iff, mem_closedBall, Complex.dist_eq, sub_zero] at m
        have ct : Tendsto (fun x ‚Ü¶ (c, x)) (ùìù x) (ùìù (c, x)) :=
          continuousAt_const.prod continuousAt_id
        by_cases x0 : x ‚â† 0
        ¬∑ rw [m.1]; rcases g.point m.2 with ‚ü®r', e, rr‚ü©
          use uncurry r'; constructor
          ¬∑ have t : ContinuousAt (fun y : ‚ÑÇ √ó ‚ÑÇ ‚Ü¶ y.2) (c, x) := continuousAt_snd
            refine' e.eventually_nhds.mp ((t.eventually_ne x0).mp (eventually_of_forall _))
            intro y y0 e
            exact
              { eqn := e
                start := fun h ‚Ü¶ (y0 h).elim }
          ¬∑ refine' ct.frequently (rr.mp (eventually_of_forall _)); intro x ‚ü®m, e‚ü©
            simp only [mem_prod_eq, mem_singleton_iff, eq_self_iff_true, true_and_iff]; use m, e
        ¬∑ use uncurry r; simp only [not_not] at x0
          simp only [m.1, x0, eq_self_iff_true, and_true_iff] at ct ‚ä¢; constructor
          ¬∑ refine'
              (g.eqn.filter_mono (nhds_le_nhdsSet _)).eventually_nhds.mp
                (eventually_of_forall fun y e ‚Ü¶ _)
            use rfl, mem_ball_self g.pos; simp only [Function.curry_uncurry]
            exact
              { eqn := e
                start := by
                  simp only [Filter.EventuallyEq.refl, imp_true_iff, Filter.eventually_true] }
          ¬∑ refine' ct.frequently (Filter.Eventually.frequently _)
            simp only [mem_prod_eq, mem_singleton_iff, eq_self_iff_true, true_and_iff]
            exact isOpen_ball.eventually_mem (mem_ball_self g.pos)
      unique := by
        intro r0 r1 t _ pre e0 e1 r01
        have u := eqns_unique pre e0 e1 ?_
        simp only [Function.uncurry_curry] at u; exact u
        simp only [Function.uncurry_curry]; exact r01 }
  have m0 : (c, (0 : ‚ÑÇ)) ‚àà ({c} √óÀ¢ ball 0 p : Set (‚ÑÇ √ó ‚ÑÇ)) := by
    simp only [mem_prod_eq, mem_singleton_iff, eq_self_iff_true, true_and_iff, mem_ball_self g.pos]
  use curry b.u
  exact
    { nonneg := g.pos.le
      zero := by rw [curry, b.uf.self_set _ m0, uncurry, g.zero]
      start := by
        refine' g.start.mp ((b.uf.filter_mono (nhds_le_nhdsSet m0)).mp (eventually_of_forall _))
        intro x e b; simp only [curry, uncurry, Prod.mk.eta] at e ‚ä¢; rw [e]; exact b
      eqn := by
        have fp := b.up
        simp only [closure_prod_eq, closure_singleton, closure_ball _ g.pos.ne'] at fp
        exact fp.mp (eventually_of_forall fun x e ‚Ü¶ e.eqn.self) }

/-- Given a increasing sequence of `p`s with corresponding `r`s and `Grow`s, we can piece together
    a single, globally consistent `r`. -/
theorem join_r (s : Super f d a) {p : ‚Ñï ‚Üí ‚Ñù} {n : ‚Ñï ‚Üí ‚Ñï} {ps : ‚Ñù} {r : ‚Ñï ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S}
    (g : ‚àÄ k, Grow s c (p k) (n k) (r k)) (mono : Monotone p)
    (tend : Tendsto p atTop (ùìù ps)) :
    ‚àÉ rs : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S, ‚àÄ (k) (x : ‚ÑÇ), abs x < p k ‚Üí uncurry rs =·∂†[ùìù (c, x)] uncurry (r k) := by
  have above : ‚àÄ k, p k ‚â§ ps := fun k ‚Ü¶ mono.ge_of_tendsto tend k
  generalize hrs : (fun e x : ‚ÑÇ ‚Ü¶
    if h : abs x < ps then r (Nat.find (tend.exists_lt h)) e x else a) = rs
  use rs
  -- rs is locally each r, via induction
  have loc : ‚àÄ k, ‚àÄ·∂† e in ùìù c, ‚àÄ x : ‚ÑÇ, abs x < p k ‚Üí rs e x = r k e x := by
    intro k; induction' k with k h
    ¬∑ apply eventually_of_forall; intro e x x0
      have xe : ‚àÉ k, abs x < p k := ‚ü®0, x0‚ü©
      simp only [‚Üê hrs, lt_of_lt_of_le x0 (above _), dif_pos, (Nat.find_eq_zero xe).mpr x0]
    ¬∑ have eq := (g k).unique (g (k + 1)) (mono (Nat.lt_succ_self _).le)
      simp only [nhdsSet_prod isCompact_singleton (isCompact_closedBall _ _)] at eq
      apply h.mp
      rcases Filter.mem_prod_iff.mp eq with ‚ü®u0, n0, u1, n1, eq‚ü©
      simp only [nhdsSet_singleton] at n0
      refine' Filter.eventually_of_mem n0 fun e eu h x xk1 ‚Ü¶ _
      by_cases xk0 : abs x < p k
      ¬∑ have m : (e, x) ‚àà u0 √óÀ¢ u1 := by
          refine' mk_mem_prod eu (subset_of_nhdsSet n1 _)
          simp only [mem_closedBall, Complex.dist_eq, sub_zero, xk0.le]
        specialize eq m; simp only [mem_setOf, uncurry] at eq
        rw [h _ xk0, eq]
      ¬∑ have xe : ‚àÉ k, abs x < p k := ‚ü®k + 1, xk1‚ü©
        have n := (Nat.find_eq_iff xe).mpr ‚ü®xk1, ?_‚ü©
        simp only [‚Üê hrs, lt_of_lt_of_le xk1 (above _), dif_pos, n]
        intro j jk; simp only [not_lt, Nat.lt_succ_iff] at jk xk0 ‚ä¢
        exact _root_.trans (mono jk) xk0
  -- rs is locally each r, final form
  intro k x xk
  rcases eventually_nhds_iff.mp (loc k) with ‚ü®u, eq, uo, uc‚ü©
  have m : u √óÀ¢ ball (0 : ‚ÑÇ) (p k) ‚àà ùìù (c, x) := by
    refine' prod_mem_nhds (uo.mem_nhds uc) (isOpen_ball.mem_nhds _)
    simp only [mem_ball, Complex.dist_eq, sub_zero, xk]
  apply Filter.eventually_of_mem m; intro ‚ü®e, y‚ü© ‚ü®m0, m1‚ü©
  simp only [mem_ball, Complex.dist_eq, sub_zero] at m1
  exact eq _ m0 _ m1

/-- If we can `Grow` up to any `q < p`, we get a `GrowOpen` up to `p` -/
theorem joined_growOpen (s : Super f d a) {p : ‚Ñï ‚Üí ‚Ñù} {ps : ‚Ñù} {r : ‚Ñï ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S} {rs : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S}
    (g : ‚àÄ k, Grow s c (p k) (s.np c ps) (r k)) (tend : Tendsto p atTop (ùìù ps))
    (post : ps < s.p c) (pos : 0 < ps)
    (loc : ‚àÄ (k) (x : ‚ÑÇ), abs x < p k ‚Üí uncurry rs =·∂†[ùìù (c, x)] uncurry (r k)) :
    GrowOpen s c ps rs :=
  { pos
    post
    zero := by
      rcases tend.exists_lt pos with ‚ü®k, pos‚ü©
      have e := (loc k 0 (by simp only [Complex.abs.map_zero, pos])).self
      simp only [uncurry] at e; simp only [e, (g k).zero]
    start := by
      rcases tend.exists_lt pos with ‚ü®k, pos‚ü©
      apply (g k).start.mp
      apply (loc k 0 (by simp only [Complex.abs.map_zero, pos])).mp
      apply eventually_of_forall; intro ‚ü®e, x‚ü© loc start
      simp only [uncurry] at loc start ‚ä¢; simp only [start, loc]
    eqn := by
      apply mem_nhdsSet_iff_forall.mpr; intro ‚ü®c', x‚ü© lt
      simp only [mem_prod_eq, mem_singleton_iff, mem_ball, Complex.dist_eq, sub_zero] at lt
      simp only [lt.1, eq_self_iff_true, true_and_iff, ‚Üê Filter.eventually_iff] at lt ‚ä¢; clear c'
      rcases tend.exists_lt lt with ‚ü®k, ltp‚ü©
      have m : (c, x) ‚àà {c} √óÀ¢ closedBall (0 : ‚ÑÇ) (p k) := by
        simp only [mem_prod_eq, mem_singleton_iff, Metric.mem_closedBall, eq_self_iff_true,
          true_and_iff, Complex.dist_eq, sub_zero, ltp.le]
      have lt' : ‚àÄ·∂† y : ‚ÑÇ √ó ‚ÑÇ in ùìù (c, x), abs y.2 < ps :=
        (Complex.continuous_abs.continuousAt.comp continuousAt_snd).eventually_lt
          continuousAt_const lt
      apply ((g k).eqn.filter_mono (nhds_le_nhdsSet m)).mp
      apply (loc _ _ ltp).eventually_nhds.mp
      apply lt'.mp
      apply eventually_of_forall; intro ‚ü®e, y‚ü© _ loc eq
      exact eq.congr (Filter.EventuallyEq.symm loc) }

/-- We can grow up to the postcritical value `s.p c` -/
theorem Super.grow (s : Super f d a) [OnePreimage s] :
    ‚àÄ p, 0 ‚â§ p ‚Üí p < s.p c ‚Üí ‚àÉ r, Grow s c p (s.np c p) r := by
  set t : Set ‚Ñù := {p | 0 ‚â§ p ‚àß ‚àÄ q, 0 ‚â§ q ‚Üí q ‚â§ p ‚Üí ‚àÉ r, Grow s c q (s.np c q) r}
  have self : ‚àÄ {p}, p ‚àà t ‚Üí ‚àÉ r, Grow s c p (s.np c p) r := fun {p} m ‚Ü¶ m.2 _ m.1 (le_refl _)
  have t1 : ‚àÄ p : ‚Ñù, p ‚àà t ‚Üí p < 1 := by intro p m; rcases self m with ‚ü®r, g‚ü©; exact g.p1
  have above : BddAbove t := bddAbove_def.mpr ‚ü®1, fun p m ‚Ü¶ (t1 p m).le‚ü©
  rcases s.grow_start c with ‚ü®p0, r0, pos0, g0‚ü©
  have start : p0 ‚àà t := by
    use g0.nonneg; intro q q0 qp; use r0; exact (g0.anti q0 qp).mono (Nat.zero_le _)
  have ne : t.Nonempty := ‚ü®p0, start‚ü©
  have pos : 0 < sSup t := lt_csSup_of_lt above start pos0
  by_cases missing : sSup t ‚àà t
  ¬∑ -- Contradict by growing a bit beyond Sup t
    rcases self missing with ‚ü®r, g‚ü©; rcases g.open with ‚ü®p, sp, g'‚ü©
    suffices m : p ‚àà t; linarith [le_csSup above m]
    use g'.self.nonneg
    intro q q0 qp; by_cases le : q ‚â§ sSup t; exact missing.2 _ q0 le
    use r; simp only [not_le] at le
    exact (g'.self.anti q0 qp).mono (s.np_mono c le.le (lt_of_le_of_lt qp g'.self.p1))
  by_cases post : sSup t < s.p c
  ¬∑ exfalso; apply missing; use pos.le; intro q q0 le
    -- q < Sup t is trivial
    by_cases lt : q < sSup t
    ¬∑ rcases exists_lt_of_lt_csSup ne lt with ‚ü®q', ‚ü®_, m‚ü©, qq‚ü©
      exact m _ q0 qq.le
    have eq := le_antisymm le (not_lt.mp lt); rw [eq]; clear eq lt le q0 q
    -- Piece together a single r that works < Sup t, then close to Sup t
    rcases exists_seq_tendsto_sSup ne above with ‚ü®p, mono, tend, sub‚ü©
    simp only [Set.range_subset_iff, mem_setOf] at sub
    set pr := fun k ‚Ü¶ choose (self (sub k))
    have pg : ‚àÄ k, Grow s c (p k) (s.np c (sSup t)) (pr k) := fun k ‚Ü¶
      (choose_spec (self (sub k))).mono
        (s.np_mono c (le_csSup above (sub k)) (lt_of_lt_of_le post s.p_le_one))
    rcases join_r s pg mono tend with ‚ü®r, loc‚ü©
    exact (joined_growOpen s pg tend post pos loc).grow
  -- Finish!
  simp only [not_lt] at post
  intro p p0 lt
  rcases exists_lt_of_lt_csSup ne (lt_of_lt_of_le lt post) with ‚ü®q, m, pq‚ü©
  exact m.2 _ p0 pq.le

/-- There is a single `r` that achieves all `Grow`s for all `c` and `p < s.p c`.

    That is, there exists a map on `ùìùÀ¢ ({c} √óÀ¢ ball 0 (s.p c))` which everywhere looks
    like an inverse to B√∂ttcher coordinates, and thus defines external rays up to the
    critical potential `s.p c`. -/
theorem Super.has_ray (s : Super f d a) [OnePreimage s] :
    ‚àÉ r : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí S, ‚àÄ c p, 0 ‚â§ p ‚Üí p < s.p c ‚Üí Grow s c p (s.np c p) r := by
  generalize hr : (fun {c p} (h : 0 ‚â§ p ‚àß p < s.p c) ‚Ü¶ choose (s.grow _ h.1 h.2)) = r
  have g : ‚àÄ {c p} (h : 0 ‚â§ p ‚àß p < s.p c), Grow s c p (s.np c p) (r h) := by
    intro c p h; rw [‚Üê hr]; exact choose_spec _
  clear hr
  generalize hray : (fun c x : ‚ÑÇ ‚Ü¶
    if h : abs x < s.p c then r ‚ü®Complex.abs.nonneg _, h‚ü© c x else a) = ray
  have loc : ‚àÄ {c p} (h : 0 ‚â§ p ‚àß p < s.p c),
      uncurry ray =·∂†[ùìùÀ¢ ({c} √óÀ¢ closedBall 0 p)] uncurry (r h) := by
    intro c p h
    rcases(g h).open with ‚ü®q', pq', gh‚ü©
    rcases exists_between (lt_min pq' h.2) with ‚ü®q, pq, qlo‚ü©
    rcases lt_min_iff.mp qlo with ‚ü®qq', qs‚ü©
    have q0 : 0 ‚â§ q := _root_.trans h.1 pq.le
    replace gh := gh.mp (eventually_of_forall fun c' g ‚Ü¶ g.anti q0 qq'.le)
    clear qlo qq' pq' q'
    rcases eventually_nhds_iff.mp gh with ‚ü®t0, gh, ot0, ct0‚ü©
    rcases eventually_nhds_iff.mp (s.lowerSemicontinuous_p _ _ qs) with ‚ü®t1, lo, ot1, ct1‚ü©
    refine eventually_nhdsSet_iff.mpr
        ‚ü®(t0 ‚à© t1) √óÀ¢ ball 0 q, (ot0.inter ot1).prod isOpen_ball, ?_, ?_‚ü©
    ¬∑ exact prod_mono (singleton_subset_iff.mpr ‚ü®ct0, ct1‚ü©) (Metric.closedBall_subset_ball pq)
    ¬∑ intro ‚ü®e, x‚ü© ‚ü®‚ü®et0, et1‚ü©, xq‚ü©; simp only [uncurry] at et0 et1 xq ‚ä¢
      simp only [mem_ball, Complex.dist_eq, sub_zero] at xq
      have hx : 0 ‚â§ abs x ‚àß abs x < s.p e := ‚ü®Complex.abs.nonneg _, _root_.trans xq (lo _ et1)‚ü©
      simp only [‚Üê hray, dif_pos hx.2]
      refine ((g hx).unique (gh _ et0) xq.le).self_set ‚ü®e, x‚ü© ‚ü®rfl, ?_‚ü©
      simp only [mem_closedBall, Complex.dist_eq, sub_zero, le_refl]
  use ray; intro c p p0 h
  exact (g ‚ü®p0, h‚ü©).congr (loc ‚ü®p0, h‚ü©).symm
