/-
Copyright (c) 2023 Geoffrey Irving. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Geoffrey Irving
-/
import Mathlib.Topology.AlexandrovDiscrete
import Mathlib.branch.GeoffreyIrving.Ray.AnalyticManifold.AnalyticManifold
import Mathlib.branch.GeoffreyIrving.Ray.AnalyticManifold.Inverse
import Mathlib.branch.GeoffreyIrving.Ray.AnalyticManifold.Nontrivial
import Mathlib.branch.GeoffreyIrving.Ray.AnalyticManifold.OneDimension
import Mathlib.branch.GeoffreyIrving.Ray.Dynamics.BottcherNearM
import Mathlib.branch.GeoffreyIrving.Ray.Misc.Topology
import Mathlib.branch.GeoffreyIrving.Ray.Tactic.Bound

/-!
## The potential map for a superattracting fixpoint

Let `s : Super f d a`, so that `a` is a superattracting fixpoint of `f c` of order d.
`Bottcher.lean` defines local B√∂ttcher coordinates `s.bottcherNear` near `a`.

Throughout the basin of attraction of `f` to `a`, we define a `[0,1)`-valued `s.potential`
function that measures how fast `f`-iteration converges to `a`.  We define `s.potential c z = 1`
if `z` doesn't attract to `a`, to give a `[0,1]`-valued map defined everywhere in the manifold.
`s.potential` is `‚Ñù`-valued rather than `‚ÑÇ`-valued since it is defined via iterated `d`th roots,
which may not have globally continuously definable argument.

If `a` has no preimages under `f c` besides itself (`OnePreimage s`), then `s.potential` is
continuous everywhere.  This is true for the Mandelbrot and Multibrot sets, but is not true
for the Newton fractal of `z ‚Ü¶ z^3 - 1` for example: `s.potential c z = 0` if `z` is an exact
iterated preimage of `a`, but such points cluster near `z = 0` with `s.potential c 0 = 1`.

## Removing the one preimage constraint

The `OnePreimage s` can be replaced by restricting to the basin of attraction.  This is mostly
straightforward, but requires working over noncompact manifolds, using compactness of levelsets
of `s.potential`.
-/

-- Remove once https://github.com/leanprover/lean4/issues/2220 is fixed
local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y)

open Classical
open Complex (exp log abs cpow)
open Filter (Tendsto atTop eventually_of_forall)
open Function (curry uncurry)
open Metric (ball closedBall isOpen_ball ball_mem_nhds mem_ball_self nonempty_ball)
open Nat (iterate)
open OneDimension
open Set
open scoped NNReal Topology
noncomputable section

-- All information for a monic superattracting fixed point at the origin
variable {S : Type} [TopologicalSpace S] [CompactSpace S] [T3Space S] [ChartedSpace ‚ÑÇ S]
  [AnalyticManifold I S]
variable {f : ‚ÑÇ ‚Üí S ‚Üí S}
variable {c : ‚ÑÇ}
variable {a z : S}
variable {d n : ‚Ñï}
variable {p : ‚ÑÇ √ó S}

/-- The potential function if `z` reaches s.near after `n` iterations -/
def Super.potential' (s : Super f d a) (c : ‚ÑÇ) (z : S) (n : ‚Ñï) : ‚Ñù :=
  abs (s.bottcherNear c ((f c)^[n] z)) ^ ((d : ‚Ñù) ^ n)‚Åª¬π

/-- `s.potential'` doesn't depend on `n`, for large `n` -/
theorem Super.potential_eq' (s : Super f d a) {c : ‚ÑÇ} {z : S} {n0 n1 : ‚Ñï}
    (n0s : (c, (f c)^[n0] z) ‚àà s.near) (n1s : (c, (f c)^[n1] z) ‚àà s.near) :
    s.potential' c z n0 = s.potential' c z n1 := by
  suffices h : ‚àÄ {n0 n1}, n0 ‚â§ n1 ‚Üí (c, (f c)^[n0] z) ‚àà s.near ‚Üí
      ((c, (f c)^[n1] z) ‚àà s.near) ‚Üí s.potential' c z n0 = s.potential' c z n1 by
    by_cases n01 : n0 ‚â§ n1
    ¬∑ exact h n01 n0s n1s
    ¬∑ exact (h (not_le.mp n01).le n1s n0s).symm
  clear n0s n1s n0 n1; intro n0 n1 n01 n0s _; rw [‚Üê Nat.sub_add_cancel n01]
  have m : ‚àÄ k, (c, (f c)^[n0 + k] z) ‚àà s.near := by
    intro k; rw [Nat.add_comm]
    simp only [Function.iterate_add, s.iter_stays_near n0s k, Function.comp]
  generalize hk : n1 - n0 = k; rw [Nat.add_comm]; clear hk
  induction' k with k h
  ¬∑ simp only [Nat.zero_eq, add_zero]
  ¬∑ simp only [Nat.add_succ, Function.iterate_succ', Super.potential', Function.comp]
    rw [s.bottcherNear_eqn (m k)]
    rw [pow_succ _ (n0 + k), mul_inv, Complex.abs.map_pow, Real.rpow_mul, ‚Üê Real.rpow_nat_cast _ d]
    rw [‚Üê Real.rpow_mul (Complex.abs.nonneg _) _ d‚Åª¬π,
      mul_inv_cancel (s.superAtC.s (Set.mem_univ c)).drz, Real.rpow_one]
    exact h; bound

/-- `s.potential c z` measures how quickly `z` attracts to `a` under `f c`. -/
@[pp_dot] def Super.potential (s : Super f d a) (c : ‚ÑÇ) (z : S) : ‚Ñù :=
  if q : ‚àÉ n, (c, (f c)^[n] z) ‚àà s.near then s.potential' c z (Nat.find q) else 1

/-- `s.potential = s.potential'` for large `n` -/
theorem Super.potential_eq (s : Super f d a) {k : ‚Ñï} (ks : (c, (f c)^[k] z) ‚àà s.near) :
    s.potential c z = s.potential' c z k := by
  have h : ‚àÉ k, (c, (f c)^[k] z) ‚àà s.near := ‚ü®k,ks‚ü©
  simp only [Super.potential, h, dif_pos]
  exact s.potential_eq' (Nat.find_spec h) ks

/-- `abs bottcherNear` in terms of `potential` -/
theorem Super.abs_bottcherNear (s : Super f d a) {n : ‚Ñï} (r : (c, (f c)^[n] z) ‚àà s.near) :
    abs (s.bottcherNear c ((f c)^[n] z)) = s.potential c z ^ d ^ n := by
  simp only [s.potential_eq r, Super.potential']
  rw [‚Üê Real.rpow_nat_cast, ‚Üê Real.rpow_mul (Complex.abs.nonneg _), Nat.cast_pow, inv_mul_cancel,
    Real.rpow_one]
  exact pow_ne_zero _ (Nat.cast_ne_zero.mpr s.d0)

/-- `potential a = 0` -/
theorem Super.potential_a (s : Super f d a) : s.potential c a = 0 := by
  have r : (c, (f c)^[0] a) ‚àà s.near := by simp only [Function.iterate_zero, s.mem_near, id.def]
  simp only [s.potential_eq r, Super.potential', Function.iterate_zero, id.def, s.bottcherNear_a,
    Complex.abs.map_zero, pow_zero, inv_one, Real.rpow_one]

/-- If `z` doesn't reach `s.near`, `potential = 1` -/
theorem Super.potential_eq_one (s : Super f d a) (a : ‚àÄ n, (c, (f c)^[n] z) ‚àâ s.near) :
    s.potential c z = 1 := by
  simp only [Super.potential, not_exists.mpr a, not_false_iff, dif_neg, and_false_iff]

/-- If `z` reaches `s.near`, `potential < 1` -/
theorem Super.potential_lt_one (s : Super f d a) (a : ‚àÉ n, (c, (f c)^[n] z) ‚àà s.near) :
    s.potential c z < 1 := by
  simp only [Super.potential, a, dif_pos, Super.potential']
  refine Real.rpow_lt_one (Complex.abs.nonneg _) ?_ (by bound [s.dp])
  exact s.bottcherNear_lt_one (Nat.find_spec a)

/-- `z` reaches `s.near` iff `potential < 1` -/
theorem Super.potential_lt_one_iff (s : Super f d a) :
    s.potential c z < 1 ‚Üî ‚àÉ n, (c, (f c)^[n] z) ‚àà s.near := by
  refine' ‚ü®_, s.potential_lt_one‚ü©
  intro h; contrapose h; simp only [not_exists] at h
  simp only [s.potential_eq_one h, lt_self_iff_false, not_false_iff]

/-- `potential ‚â§ 1` -/
theorem Super.potential_le_one (s : Super f d a) : s.potential c z ‚â§ 1 := by
  by_cases a : ‚àÉ n, (c, (f c)^[n] z) ‚àà s.near
  exact (s.potential_lt_one a).le
  exact le_of_eq (s.potential_eq_one (not_exists.mp a))

/-- `0 ‚â§ potential` -/
theorem Super.potential_nonneg (s : Super f d a) : 0 ‚â§ s.potential c z := by
  by_cases r : ‚àÉ n, (c, (f c)^[n] z) ‚àà s.near
  rcases r with ‚ü®n, r‚ü©; simp only [s.potential_eq r, Super.potential']; bound
  simp only [s.potential_eq_one (not_exists.mp r), zero_le_one]

/-- The defining equation of `s.potential` -/
theorem Super.potential_eqn (s : Super f d a) : s.potential c (f c z) = s.potential c z ^ d := by
  by_cases a : ‚àÉ n, (c, (f c)^[n] z) ‚àà s.near
  ¬∑ rcases a with ‚ü®n, a‚ü©
    have a' : (c, (f c)^[n] (f c z)) ‚àà s.near := by
      simp only [‚Üê Function.iterate_succ_apply, Function.iterate_succ', s.stays_near a,
        Function.comp]
    simp only [s.potential_eq a, s.potential_eq a', Super.potential', ‚Üê Function.iterate_succ_apply,
      Function.iterate_succ', s.bottcherNear_eqn a, Complex.abs.map_pow, ‚Üê Real.rpow_nat_cast, ‚Üê
      Real.rpow_mul (Complex.abs.nonneg _), mul_comm, Function.comp]
  ¬∑ have a' : ‚àÄ n, (c, (f c)^[n] (f c z)) ‚àâ s.near := by
      contrapose a; simp only [not_forall, not_not, ‚Üê Function.iterate_succ_apply] at a ‚ä¢
      rcases a with ‚ü®n, a‚ü©; exact ‚ü®n + 1, a‚ü©
    simp only [s.potential_eq_one (not_exists.mp a), s.potential_eq_one a', one_pow]

/-- The potential equation, iterated -/
theorem Super.potential_eqn_iter (s : Super f d a) (n : ‚Ñï) :
    s.potential c ((f c)^[n] z) = s.potential c z ^ d ^ n := by
  induction' n with n h
  ¬∑ simp only [Function.iterate_zero, id.def, pow_zero, pow_one]
  ¬∑ simp only [Function.iterate_succ', Super.potential_eqn, h, ‚Üê pow_mul, ‚Üê pow_succ',
      Function.comp]

/-- Our standard iteration is analytic -/
theorem Super.iter_holomorphic' (s : Super f d a) (n : ‚Ñï) :
    Holomorphic II I fun p : ‚ÑÇ √ó S ‚Ü¶ (f p.1)^[n] p.2 := by
  intro p; induction' n with n h; simp [Function.iterate_zero, holomorphicAt_snd]
  simp only [Function.iterate_succ', Function.comp]
  exact (s.fa _).curry_comp holomorphicAt_fst h

theorem Super.iter_holomorphic (s : Super f d a) (n : ‚Ñï) :
    Holomorphic II II fun p : ‚ÑÇ √ó S ‚Ü¶ (p.1, (f p.1)^[n] p.2) := by
  intro p; apply holomorphicAt_fst.prod; apply s.iter_holomorphic'

/-- `s.potential` is continuous where we attract -/
theorem ContinuousAt.potential_of_reaches (s : Super f d a) (a : ‚àÉ n, (c, (f c)^[n] z) ‚àà s.near) :
    ContinuousAt (uncurry s.potential) (c, z) := by
  rcases a with ‚ü®n, a‚ü©
  have e : uncurry s.potential =·∂†[ùìù (c, z)] fun p : ‚ÑÇ √ó S ‚Ü¶ s.potential' p.1 p.2 n := by
    have a' : ‚àÄ·∂† p : ‚ÑÇ √ó S in ùìù (c, z), (p.1, (f p.1)^[n] p.2) ‚àà s.near :=
      (s.iter_holomorphic n _).continuousAt.eventually_mem s.isOpen_near a
    refine' a'.mp (eventually_of_forall fun p h ‚Ü¶ _)
    simp only [uncurry, s.potential_eq h]
  simp only [continuousAt_congr e, Super.potential']
  refine' ContinuousAt.rpow _ continuousAt_const _
  ¬∑ apply Complex.continuous_abs.continuousAt.comp
    refine' ((s.bottcherNear_holomorphic _ _).comp (s.iter_holomorphic n (c, z))).continuousAt
    exact a
  ¬∑ right; bound [s.dp]

/-- `s.potential = 0` exactly on iterated preimages of `a` -/
theorem Super.potential_eq_zero (s : Super f d a) : s.potential c z = 0 ‚Üî ‚àÉ n, (f c)^[n] z = a := by
  constructor
  ¬∑ intro h
    by_cases r : ‚àÉ n, (c, (f c)^[n] z) ‚àà s.near
    ¬∑ rcases r with ‚ü®n, r‚ü©
      simp only [s.potential_eq r, Super.potential',
        Real.rpow_eq_zero_iff_of_nonneg (Complex.abs.nonneg _), Complex.abs.eq_zero,
        s.bottcherNear_eq_zero r] at h
      use n, h.1
    ¬∑ rw [not_exists] at r; simp only [s.potential_eq_one r, one_ne_zero] at h
  ¬∑ intro p; rcases p with ‚ü®n, p‚ü©
    have nz : d ^ n > 0 := pow_pos s.dp _
    rw [‚Üê pow_eq_zero_iff nz, ‚Üê s.potential_eqn_iter n, p, s.potential_a]

/-- `s.potential` is upper semicontinuous unconditionally -/
theorem UpperSemicontinuous.potential (s : Super f d a) :
    UpperSemicontinuous (uncurry s.potential) := by
  intro ‚ü®c, z‚ü©
  by_cases r : ‚àÉ n, (c, (f c)^[n] z) ‚àà s.near
  ¬∑ exact (ContinuousAt.potential_of_reaches s r).upperSemicontinuousAt
  ¬∑ simp only [uncurry, UpperSemicontinuousAt, s.potential_eq_one (not_exists.mp r)]
    exact fun y y1 ‚Ü¶ eventually_of_forall fun p ‚Ü¶ lt_of_le_of_lt s.potential_le_one y1

/-- Potential is everywhere continuous only using an additional assumption.  The most general
    assumption is that the set of preimages is closed, but for the Mandelbrot set we have the
    simpler case that `a` is the only preimage of `a`. -/
class OnePreimage (s : Super f d a) : Prop where
  eq_a : ‚àÄ c z, f c z = a ‚Üí z = a

theorem Super.preimage_eq' (s : Super f d a) [o : OnePreimage s] : f c z = a ‚Üî z = a := by
  have e := o.eq_a c z; refine' ‚ü®e, _‚ü©; intro e; simp only [e, s.f0]

theorem Super.preimage_eq (s : Super f d a) [o : OnePreimage s] {n : ‚Ñï} :
    (f c)^[n] z = a ‚Üî z = a := by
  induction' n with n h; simp only [Function.iterate_zero_apply]
  simp only [Function.iterate_succ_apply', s.preimage_eq', h]

theorem Super.potential_eq_zero_of_onePreimage (s : Super f d a) [OnePreimage s] (c : ‚ÑÇ) :
    s.potential c z = 0 ‚Üî z = a := by
  constructor
  ¬∑ intro h; rw [s.potential_eq_zero] at h; rcases h with ‚ü®n, h‚ü©; rw [s.preimage_eq] at h; exact h
  ¬∑ intro h; simp only [h, s.potential_a]

theorem Super.potential_ne_zero (s : Super f d a) [OnePreimage s] (c : ‚ÑÇ) :
    s.potential c z ‚â† 0 ‚Üî z ‚â† a := by simp only [Ne.def, s.potential_eq_zero_of_onePreimage]

theorem Super.potential_pos (s : Super f d a) [OnePreimage s] (c : ‚ÑÇ) :
    0 < s.potential c z ‚Üî z ‚â† a := by
  rw [‚Üê s.potential_ne_zero c]
  use ne_of_gt, fun ne ‚Ü¶ ne.symm.lt_of_le s.potential_nonneg

/-- `f` can't get from far from `(c,a)` to arbitrarily close to `(c,a)` in one step -/
theorem Super.no_jump (s : Super f d a) [OnePreimage s] (c : ‚ÑÇ) (n : Set (‚ÑÇ √ó S)) (no : IsOpen n)
    (na : (c, a) ‚àà n) : ‚àÄ·∂† p : ‚ÑÇ √ó S in ùìù (c, a), ‚àÄ q, p = s.fp q ‚Üí q ‚àà n := by
  have h : ‚àÄ q : ‚ÑÇ √ó S, f q.1 q.2 = a ‚Üí q.2 = a := fun _ ‚Ü¶ by simp only [s.preimage_eq', imp_self]
  contrapose h
  simp only [Filter.not_eventually, not_forall, exists_prop] at h
  set t := s.fp '' (closedBall c 1 √óÀ¢ univ ‚à© n·∂ú)
  have tc : IsClosed t := by
    refine' (IsCompact.image _ s.fpa.continuous).isClosed
    exact ((isCompact_closedBall _ _).prod isCompact_univ).inter_right no.isClosed_compl
  have th : ‚àÉ·∂† p in ùìù (c, a), p ‚àà t := by
    have mb : ‚àÄ·∂† p : ‚ÑÇ √ó S in ùìù (c, a), p.1 ‚àà closedBall c 1 :=
      continuousAt_fst.eventually_mem_nhd (Metric.closedBall_mem_nhds _ zero_lt_one)
    refine' (h.and_eventually mb).mp (eventually_of_forall fun p i ‚Ü¶ _)
    rcases i with ‚ü®‚ü®q, qp, m‚ü©, b‚ü©
    simp only [Prod.ext_iff] at qp; simp only [qp.1] at b
    simp only [Set.mem_image, Set.mem_compl_iff, Set.mem_inter_iff, Set.mem_prod_eq, Set.mem_univ,
      and_true_iff, Prod.ext_iff]
    use q, ‚ü®b, m‚ü©, qp.1.symm, qp.2.symm
  have m := th.mem_of_closed tc
  rcases(Set.mem_image _ _ _).mp m with ‚ü®p, m, pa‚ü©
  simp only [Super.fp, Prod.mk.inj_iff] at pa
  simp only [not_forall]; use p, pa.2
  contrapose m; simp only [not_not, Set.mem_compl_iff] at m ‚ä¢
  rw [‚Üê @Prod.mk.eta _ _ p, pa.1, m]
  simp only [Set.mem_inter_iff, Set.prod_mk_mem_set_prod_eq, Metric.mem_closedBall, dist_self,
    zero_le_one, Set.mem_univ, Set.mem_compl_iff, true_and_iff, Set.not_not_mem, not_not,
    na]

/-- A barrier is a compact, annular region around `a` (but not containing it) such that
    outside points must pass through it to reach `a`. -/
structure Barrier (s : Super f d a) (c : ‚ÑÇ) (n t : Set (‚ÑÇ √ó S)) : Prop where
  compact : IsCompact t
  tn : t ‚äÜ n
  near : t ‚äÜ s.near
  hole : ‚àÄ e, (e, a) ‚àâ t
  barrier : ‚àÄ·∂† e in ùìù c, ‚àÄ z, (e, z) ‚àâ n ‚Üí Attracts (f e) z a ‚Üí ‚àÉ n, (e, (f e)^[n] z) ‚àà t

/-- `f` can't get from far from `(c,a)` to close to `(c,a)` without passing through a barrier -/
theorem Super.barrier (s : Super f d a) [OnePreimage s] (n : Set (‚ÑÇ √ó S)) (no : IsOpen n)
    (na : (c, a) ‚àà n) : ‚àÉ t : Set (‚ÑÇ √ó S), Barrier s c n t := by
  set n' := n ‚à© s.near
  have nn' : n' ‚àà ùìù (c, a) :=
    Filter.inter_mem (no.mem_nhds na) (s.isOpen_near.mem_nhds (s.mem_near c))
  rcases (Filter.hasBasis_iff.mp (compact_basis_nhds (c, a)) n').mp nn' with ‚ü®u, ‚ü®un, uc‚ü©, us‚ü©
  simp only [Set.subset_inter_iff] at us
  rcases eventually_nhds_iff.mp
      (s.no_jump c (interior u) isOpen_interior (mem_interior_iff_mem_nhds.mpr un)) with
    ‚ü®i, ih, io, ia‚ü©
  rcases mem_nhds_prod_iff'.mp (Filter.inter_mem un (io.mem_nhds ia)) with
    ‚ü®i0, i1, i0o, i0m, i1o, i1m, ii‚ü©
  simp only [Set.subset_inter_iff] at ii
  set t := u \ univ √óÀ¢ i1
  have ta : ‚àÄ e, (e, a) ‚àâ t := fun e ‚Ü¶
    Set.not_mem_diff_of_mem (Set.mk_mem_prod (Set.mem_univ _) i1m)
  use t
  refine' ‚ü®uc.diff (isOpen_univ.prod i1o), _root_.trans (Set.diff_subset _ _) us.1,
      _root_.trans (Set.diff_subset _ _) us.2, ta, _‚ü©
  rw [eventually_nhds_iff]; use i0; refine' ‚ü®_, i0o, i0m‚ü©
  intro e em z zm za
  rcases tendsto_atTop_nhds.mp za i1 i1m i1o with ‚ü®m, mh‚ü©
  have en : ‚àÉ n, (f e)^[n] z ‚àà i1 := ‚ü®m, mh m (le_refl _)‚ü©
  set n := Nat.find en
  use n - 1
  have ni1 : (f e)^[n] z ‚àà i1 := Nat.find_spec en
  have n0 : n ‚â† 0 := by
    contrapose zm; simp only [Set.not_not_mem]
    simp only [Nat.sub, Ne.def, Nat.find_eq_zero en, Function.iterate_zero, id.def,
      Set.not_not_mem] at zm
    exact us.1 (ii.1 (Set.mk_mem_prod em zm))
  have nt : (f e)^[n-1] z ‚àâ i1 := Nat.find_min en (Nat.pred_lt n0)
  apply Set.mem_diff_of_mem
  ¬∑ apply interior_subset; apply ih (e, (f e)^[n] z) (ii.2 (Set.mk_mem_prod em ni1))
    simp only [Super.fp]; rw [‚Üê Function.iterate_succ_apply' (f e) (n - 1)]
    simp only [Nat.succ_eq_add_one, Nat.sub_add_cancel (Nat.one_le_of_lt (Nat.pos_of_ne_zero n0))]
  ¬∑ contrapose nt
    simp only [Set.prod_mk_mem_set_prod_eq, not_and, not_forall, Set.not_not_mem,
      exists_prop] at nt ‚ä¢
    exact nt.2

/-- `s.potential` is large on barriers (because they are compact) -/
theorem Barrier.potential_large {s : Super f d a} [OnePreimage s] {n t : Set (‚ÑÇ √ó S)}
    (b : Barrier s c n t) : ‚àÉ r : ‚Ñù, r > 0 ‚àß ‚àÄ e z, (e, z) ‚àà t ‚Üí r ‚â§ s.potential e z := by
  by_cases t0 : t = ‚àÖ
  ¬∑ use 1, zero_lt_one
    simp only [t0, gt_iff_lt, Set.mem_empty_iff_false, IsEmpty.forall_iff, forall_const,
      imp_true_iff, and_true_iff]
  simp only [‚Üê Ne.def, ‚Üê Set.nonempty_iff_ne_empty] at t0
  have pc : ContinuousOn (uncurry s.potential) t := by
    refine' ContinuousOn.mono _ b.near
    intro ‚ü®c, z‚ü© m; apply ContinuousAt.continuousWithinAt
    apply ContinuousAt.potential_of_reaches s; use 0; simpa only [Function.iterate_zero_apply]
  rcases pc.compact_min b.compact t0 with ‚ü®‚ü®e, z‚ü©, ps, pm‚ü©
  use s.potential e z; constructor
  ¬∑ have h := b.hole e; contrapose h; simp only [not_lt] at h
    have h' := le_antisymm h s.potential_nonneg
    simp only [s.potential_eq_zero, s.preimage_eq, exists_const] at h'
    simp only [not_not, ‚Üê h', ps]
  ¬∑ intro e z m; simp only [isMinOn_iff, uncurry] at pm ‚ä¢; exact pm _ m

/-- The first `n` preimages of a barrier -/
@[nolint unusedArguments]
def Barrier.fast {s : Super f d a} {n t : Set (‚ÑÇ √ó S)} (_ : Barrier s c n t) (m : ‚Ñï) :
    Set (‚ÑÇ √ó S) :=
  ‚ãÉ k : Fin m, (fun p : ‚ÑÇ √ó S ‚Ü¶ (p.1, (f p.1)^[k] p.2)) ‚Åª¬π' t

theorem Barrier.closed_fast {s : Super f d a} {n t : Set (‚ÑÇ √ó S)} (b : Barrier s c n t) (m : ‚Ñï) :
    IsClosed (b.fast m) := by
  apply isClosed_iUnion_of_finite; intro k; refine' IsClosed.preimage _ b.compact.isClosed
  apply continuous_fst.prod; generalize hn : (k : ‚Ñï) = n; clear k hn; induction' n with n h
  simp only [Function.iterate_zero_apply]; exact continuous_snd
  simp only [Function.iterate_succ_apply']; exact s.fa.continuous.comp (continuous_fst.prod h)

theorem Barrier.mem_fast {s : Super f d a} {n t : Set (‚ÑÇ √ó S)} (b : Barrier s c n t) {m : ‚Ñï} {e : ‚ÑÇ}
    {z : S} : (e, z) ‚àà b.fast m ‚Üî ‚àÉ n, n < m ‚àß (e, (f e)^[n] z) ‚àà t := by
  simp only [Barrier.fast, Set.mem_iUnion, Set.mem_preimage]; constructor
  intro h; rcases h with ‚ü®n, h‚ü©; use n, Fin.is_lt _, h
  intro h; rcases h with ‚ü®n, nm, h‚ü©; use‚ü®n, nm‚ü©, h

theorem Barrier.fast_reaches {s : Super f d a} {n t : Set (‚ÑÇ √ó S)} (b : Barrier s c n t) {m : ‚Ñï}
    {e : ‚ÑÇ} {z : S} (q : (e, z) ‚àà b.fast m) : ‚àÉ n, (e, (f e)^[n] z) ‚àà s.near := by
  rw [b.mem_fast] at q; rcases q with ‚ü®n, _, q‚ü©; exact ‚ü®n, b.near q‚ü©

/-- `s.potential` is everywhere lower semicontinuous (and thus continuous) if `OnePreimage s` -/
theorem Continuous.potential (s : Super f d a) [OnePreimage s] :
    Continuous (uncurry s.potential) := by
  -- Reduce to showing that nearby bounded potential means reaches
  refine' continuous_iff_lower_upperSemicontinuous.mpr ‚ü®_, UpperSemicontinuous.potential s‚ü©
  intro ‚ü®c, z‚ü©
  by_cases re : ‚àÉ n, (c, (f c)^[n] z) ‚àà s.near
  exact (ContinuousAt.potential_of_reaches s re).lowerSemicontinuousAt
  simp only [not_exists] at re
  intro y y1
  simp only [ContinuousAt, uncurry, s.potential_eq_one re] at y1 ‚ä¢
  contrapose re
  simp only [Filter.not_eventually, not_lt] at re
  simp only [not_forall, not_not] at re ‚ä¢
  -- Construct a barrier separating (c,z) from (c,a)
  by_cases za : z = a
  ¬∑ use 0; simp only [za, Function.iterate_zero_apply, s.mem_near c]
  have sn : {(c, a)}·∂ú ‚àà ùìù (c, z) :=
    compl_singleton_mem_nhds
      (by simp only [za, Ne.def, Prod.mk.inj_iff, and_false_iff, not_false_iff])
  rcases (Filter.hasBasis_iff.mp (compact_basis_nhds (c, z)) ({(c, a)}·∂ú)).mp sn with
    ‚ü®u, ‚ü®un, uc‚ü©, ua‚ü©
  simp only [Set.subset_compl_singleton_iff] at ua
  rcases s.barrier (u·∂ú) uc.isClosed.isOpen_compl (Set.mem_compl ua) with ‚ü®t, b‚ü©
  rcases b.potential_large with ‚ü®r, rp, rt‚ü©
  -- `potential ‚â§ y ‚Üí` reaches the barrier quickly
  have en : ‚àÉ n, ‚àÄ·∂† e in ùìù c, ‚àÄ z, (e, z) ‚àà u ‚Üí s.potential e z ‚â§ y ‚Üí (e, z) ‚àà b.fast n := by
    -- Find n s.t. y ^ (d^n) < r
    rcases exists_pow_lt_of_lt_one rp y1 with ‚ü®k, ky‚ü©
    rcases Filter.exists_le_of_tendsto_atTop (Nat.tendsto_pow_atTop_atTop_of_one_lt s.d1) 0 k
      with ‚ü®n, _, nk‚ü©
    use n
    -- Our upper bound on `potential e z`, plus on our lower bound on `t`,
    -- implies that `z` reaches near quickly
    refine b.barrier.mp (eventually_of_forall fun e h z m py ‚Ü¶ ?_)
    have za : Attracts (f e) z a := by
      by_cases r : ‚àÉ n, (e, (f e)^[n] z) ‚àà s.near
      ¬∑ rcases r with ‚ü®n, r‚ü©; exact s.attracts r
      simp only [not_exists] at r; rw [s.potential_eq_one r] at py; linarith
    rcases h z (not_mem_compl_iff.mpr m) za with ‚ü®o, oh‚ü©
    by_cases no : n ‚â§ o
    ¬∑ have pyo : s.potential e z ^ d ^ o ‚â§ y ^ d ^ o := by bound [s.potential_nonneg]
      rw [‚Üê s.potential_eqn_iter o] at pyo
      have ryo : r ‚â§ y ^ d ^ o := _root_.trans (rt _ _ oh) pyo
      have kdo : k ‚â§ d ^ o := _root_.trans nk (Nat.pow_le_pow_of_le_right s.dp no)
      have ryk : r ‚â§ y ^ k :=
        _root_.trans ryo (pow_le_pow_of_le_one (_root_.trans s.potential_nonneg py) y1.le kdo)
      linarith
    ¬∑ simp only [not_le] at no; rw [b.mem_fast]; use o, no, oh
  -- Now that we've bounded n, (c,z) must reach near
  rcases en with ‚ü®n, h‚ü©
  rcases eventually_nhds_iff.mp h with ‚ü®v, vh, vo, vc‚ü©
  have ev : ‚àÄ·∂† p : ‚ÑÇ √ó S in ùìù (c, z), p ‚àà u ‚à© v √óÀ¢ univ := by
    simp only [Filter.eventually_iff, Set.setOf_mem_eq]
    exact Filter.inter_mem un ((vo.prod isOpen_univ).mem_nhds (Set.mk_mem_prod vc (Set.mem_univ _)))
  have ef : ‚àÉ·∂† p in ùìù (c, z), p ‚àà b.fast n := by
    refine' (re.and_eventually ev).mp (eventually_of_forall _)
    intro ‚ü®e, z‚ü© ‚ü®zy, m‚ü©
    simp only [Set.mem_inter_iff, Set.mem_prod, Set.mem_univ, and_true_iff] at m
    exact vh e m.2 z m.1 zy
  rcases b.mem_fast.mp (ef.mem_of_closed (b.closed_fast _)) with ‚ü®n, _, r‚ü©
  exact ‚ü®n, b.near r‚ü©

/-- potential levelsets form a neighborhood basis at `a` (open version) -/
theorem Super.potential_basis' (s : Super f d a) [OnePreimage s] (c : ‚ÑÇ) {t : Set S}
    (n : t ‚àà ùìù a) (o : IsOpen t) :
    ‚àÉ p, 0 < p ‚àß {z | s.potential c z < p} ‚äÜ t := by
  by_cases ne : t·∂ú = ‚àÖ
  ¬∑ use 1, zero_lt_one; simp only [compl_empty_iff] at ne; rw [ne]; exact subset_univ _
  replace ne := Set.Nonempty.image (s.potential c) (nonempty_iff_ne_empty.mpr ne)
  have pos : ‚àÄ p : ‚Ñù, p ‚àà s.potential c '' t·∂ú ‚Üí 0 ‚â§ p := by
    intro p m; simp only [mem_image] at m; rcases m with ‚ü®z, _, e‚ü©; rw [‚Üê e]
    exact s.potential_nonneg
  have below : BddBelow (s.potential c '' t·∂ú) := bddBelow_def.mpr ‚ü®0, pos‚ü©
  generalize hq : sInf (s.potential c '' t·∂ú) = q
  have qt : ‚àÄ z, s.potential c z < q ‚Üí z ‚àà t := by
    intro z i; contrapose i; simp only [not_lt, ‚Üê hq]; apply csInf_le below
    simp only [mem_image]; use z, i
  have qp : 0 < q := by
    simp only [‚Üê hq]
    have mc := csInf_mem_closure ne below
    rw [IsClosed.closure_eq] at mc
    ¬∑ simp only [mem_image] at mc; rcases mc with ‚ü®z, m, e‚ü©
      rw [‚Üê e]; contrapose m
      replace m := le_antisymm (not_lt.mp m) s.potential_nonneg
      rw [s.potential_eq_zero_of_onePreimage] at m; simp only [m, not_mem_compl_iff]
      exact mem_of_mem_nhds n
    ¬∑ exact (o.isClosed_compl.isCompact.image (Continuous.potential s).in2).isClosed
  use q, qp, qt

/-- potential levelsets form a neighborhood basis at `a` (general version) -/
theorem Super.potential_basis (s : Super f d a) [OnePreimage s] (c : ‚ÑÇ) {t : Set S} (n : t ‚àà ùìù a) :
    ‚àÉ p, 0 < p ‚àß {z | s.potential c z < p} ‚äÜ t := by
  rcases mem_nhds_iff.mp n with ‚ü®t', tt, o, m‚ü©
  rcases s.potential_basis' c (o.mem_nhds m) o with ‚ü®p, pp, sub‚ü©
  use p, pp, _root_.trans sub tt

/-- Fix `c`, and let `p < 1`.  Then `u = s.potential c ‚Åª¬π' Icc 0 p` is closed, and thus compact,
    and thus there is a fixed `n` s.t. `f c^[n] '' u ‚äÜ s.near`.  This lets us work with fixed `n`
    more of the time. -/
def Super.IsNiceN (s : Super f d a) (c : ‚ÑÇ) (p : ‚Ñù) (n : ‚Ñï) :=
  ‚àÄ z, s.potential c z ‚â§ p ‚Üí
    (c, (f c)^[n] z) ‚àà s.near ‚àß ‚àÄ k, n ‚â§ k ‚Üí mfderiv I I (s.bottcherNear c) ((f c)^[k] z) ‚â† 0

theorem Super.isNice_zero (s : Super f d a) (c : ‚ÑÇ) [OnePreimage s] : s.IsNiceN c 0 0 := by
  intro z zp
  have za := le_antisymm zp s.potential_nonneg
  simp only [s.potential_eq_zero_of_onePreimage] at za
  rw [za, Function.iterate_zero_apply]; use s.mem_near c
  intro k _; rw [s.iter_a]; exact s.bottcherNear_mfderiv_ne_zero c

theorem Super.isNiceN_mono (s : Super f d a) {c : ‚ÑÇ} {p : ‚Ñù} {n0 n1 : ‚Ñï} (nice : s.IsNiceN c p n0)
    (n01 : n0 ‚â§ n1) : s.IsNiceN c p n1 := by
  intro z zp; rcases nice z zp with ‚ü®m, nc‚ü©
  use s.iter_stays_near' m n01, fun k n1k ‚Ü¶ nc k (_root_.trans n01 n1k)

theorem Super.has_nice_n (s : Super f d a) (c : ‚ÑÇ) {p : ‚Ñù} (p1 : p < 1) [op : OnePreimage s] :
    ‚àÉ n, s.IsNiceN c p n := by
  have et : ‚àÄ·∂† z in ùìù a, (c, z) ‚àà s.near ‚àß mfderiv I I (s.bottcherNear c) z ‚â† 0 := by
    apply
      (mfderiv_ne_zero_eventually (s.bottcherNear_holomorphic _ (s.mem_near c)).in2
          (s.bottcherNear_mfderiv_ne_zero c)).mp
    apply ((s.isOpen_near.snd_preimage c).eventually_mem (s.mem_near c)).mp
    refine' eventually_of_forall fun z m nc ‚Ü¶ _; use m, nc
  rcases et.exists_mem with ‚ü®t, m, h‚ü©
  rcases s.potential_basis c m with ‚ü®q, qp, qt‚ü©; clear et m
  rcases exists_pow_lt_of_lt_one qp p1 with ‚ü®n, pq‚ü©
  use n; intro z m
  replace m : ‚àÄ k, n ‚â§ k ‚Üí s.potential c ((f c)^[k] z) < q
  ¬∑ intro k nk; refine' lt_of_le_of_lt _ pq; simp only [s.potential_eqn_iter]
    have dn := (Nat.lt_pow_self s.d1 k).le
    apply _root_.trans (pow_le_pow_of_le_one s.potential_nonneg s.potential_le_one dn)
    refine' _root_.trans (pow_le_pow_of_le_left s.potential_nonneg m _) _
    exact pow_le_pow_of_le_one (_root_.trans s.potential_nonneg m) p1.le nk
  use(h _ (qt (m n (le_refl _)))).1, fun k nk ‚Ü¶ (h _ (qt (m k nk))).2

/-- An `n` such that `(f c)^[n]` sends everything with potential < `p` to `s.near` -/
def Super.np (s : Super f d a) (c : ‚ÑÇ) (p : ‚Ñù) : ‚Ñï :=
  if q : p < 1 ‚àß OnePreimage s then Nat.find (Super.has_nice_n c q.1 (op := q.2)) else 0

theorem Super.nice_np (s : Super f d a) (c : ‚ÑÇ) {p : ‚Ñù} (p1 : p < 1) [op : OnePreimage s] :
    s.IsNiceN c p (s.np c p) := by
  have q : p < 1 ‚àß OnePreimage s := ‚ü®p1, op‚ü©
  simp only [Super.np, q, true_and_iff, dif_pos]
  exact Nat.find_spec (s.has_nice_n c p1)

theorem Super.np_zero (s : Super f d a) (c : ‚ÑÇ) [op : OnePreimage s] : s.np c 0 = 0 := by
  simp only [Super.np, zero_lt_one, op, true_and_iff, dif_pos, Nat.find_eq_zero, Super.isNice_zero]

theorem Super.np_mono (s : Super f d a) (c : ‚ÑÇ) {p0 p1 : ‚Ñù} (le : p0 ‚â§ p1) (p11 : p1 < 1)
    [op : OnePreimage s] : s.np c p0 ‚â§ s.np c p1 := by
  have p01 : p0 < 1 := lt_of_le_of_lt le p11
  have e : s.np c p0 = Nat.find (s.has_nice_n c p01) := by
    simp only [Super.np, p01, op, true_and_iff, dif_pos]
  rw [e]; apply Nat.find_min'; exact fun z zp ‚Ü¶ s.nice_np c p11 _ (_root_.trans zp le)

/-- An `n` such that `(f c)^[n]` sends everything with potential < `s.potential c z` to `s.near` -/
def Super.nz (s : Super f d a) (c : ‚ÑÇ) (z : S) : ‚Ñï :=
  s.np c (s.potential c z)

lemma Super.nice_nz (s : Super f d a) {c : ‚ÑÇ} {z : S} (m : (c, z) ‚àà s.basin) [OnePreimage s] :
    s.IsNiceN c (s.potential c z) (s.nz c z) :=
  s.nice_np c (s.potential_lt_one m)
