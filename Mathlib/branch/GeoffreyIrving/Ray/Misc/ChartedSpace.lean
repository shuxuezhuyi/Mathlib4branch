/-
Copyright (c) 2023 Geoffrey Irving. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Geoffrey Irving
-/
import Mathlib.Geometry.Manifold.ChartedSpace

/-!
## Charted space lemmas
-/

open ChartedSpace (chartAt)
open Set
open scoped Topology
noncomputable section

variable {A M : Type} [TopologicalSpace A] [TopologicalSpace M] [ChartedSpace A M]

/-- Charted space are T1.
    Making this an instance seems to cause infinite instance resolution loops. -/
theorem ChartedSpace.t1Space [T1Space A] : T1Space M where
  t1 := by
    intro x; rw [‚Üêcompl_compl ({x} : Set M)]; apply IsOpen.isClosed_compl
    rw [isOpen_iff_mem_nhds]; intro y m; simp only [mem_compl_singleton_iff] at m
    simp only [mem_nhds_iff, subset_compl_singleton_iff]
    by_cases xm : x ‚àà (chartAt A y).source
    ¬∑ set t := (chartAt A y).source \ {x}
      have e : t = (chartAt A y).source ‚à© chartAt A y ‚Åª¬π'
          ((chartAt A y).target \ {chartAt A y x}) := by
        apply Set.ext; intro z
        simp only [mem_diff, mem_singleton_iff, mem_inter_iff, mem_preimage]; constructor
        intro ‚ü®zm, zx‚ü©; use zm, LocalEquiv.map_source _ zm, (LocalEquiv.injOn _).ne zm xm zx
        intro ‚ü®zm, _, zx‚ü©; use zm, ((LocalEquiv.injOn _).ne_iff zm xm).mp zx
      use t; refine' ‚ü®_, _, _‚ü©
      simp only [mem_diff, mem_singleton_iff, eq_self_iff_true, not_true, and_false_iff,
        not_false_iff]
      rw [e]
      apply (chartAt A y).continuousOn.preimage_open_of_open (_root_.chartAt _ _).open_source
      exact IsOpen.sdiff (_root_.chartAt _ _).open_target isClosed_singleton
      rw [e]
      simp only [mem_inter_iff, mem_chart_source, true_and_iff, mem_preimage, mem_diff,
        mem_chart_target, mem_singleton_iff]
      exact (LocalEquiv.injOn _).ne (mem_chart_source A y) xm m
    ¬∑ use(chartAt A y).source, xm, (chartAt A y).open_source, mem_chart_source A y

/-- Charted spaces are regular if `A` is regular and locally compact and `M` is `T2`.
    This is a weird set of assumptions, but I don't know how to prove T2 naturally. -/
theorem ChartedSpace.regularSpace [T2Space M] [LocallyCompactSpace A] : RegularSpace M := by
  apply RegularSpace.ofExistsMemNhdsIsClosedSubset; intro x s n
  set t := (chartAt A x).target ‚à© (chartAt A x).symm ‚Åª¬π' ((chartAt A x).source ‚à© s)
  have cn : (chartAt A x).source ‚àà ùìù x :=
    (chartAt A x).open_source.mem_nhds (mem_chart_source A x)
  have tn : t ‚àà ùìù (chartAt A x x) := by
    apply Filter.inter_mem ((chartAt A x).open_target.mem_nhds (mem_chart_target A x))
    apply ((chartAt A x).continuousAt_symm (mem_chart_target _ _)).preimage_mem_nhds
    rw [(chartAt A x).left_inv (mem_chart_source _ _)]; exact Filter.inter_mem cn n
  rcases local_compact_nhds tn with ‚ü®u, un, ut, uc‚ü©
  refine' ‚ü®(chartAt A x).symm '' u, _, _, _‚ü©
  ¬∑ convert (chartAt A x).symm.image_mem_nhds _ un
    rw [(chartAt A x).left_inv (mem_chart_source _ _)]
    rw [LocalHomeomorph.symm_source]; exact mem_chart_target _ _
  ¬∑ have c : IsCompact ((chartAt A x).symm '' u) :=
      uc.image_of_continuousOn ((chartAt A x).continuousOn_symm.mono
        (_root_.trans ut (inter_subset_left _ _)))
    exact c.isClosed
  ¬∑ intro y m; rcases(mem_image _ _ _).mp m with ‚ü®z, zu, zy‚ü©; rw [‚Üê zy]
    rcases ut zu with ‚ü®_, m1‚ü©; simp only [mem_preimage] at m1; exact m1.2
