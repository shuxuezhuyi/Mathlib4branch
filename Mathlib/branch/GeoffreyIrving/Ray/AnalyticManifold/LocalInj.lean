/-
Copyright (c) 2023 Geoffrey Irving. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Geoffrey Irving
-/
import Mathlib.Geometry.Manifold.MFDeriv
import Mathlib.branch.GeoffreyIrving.Ray.AnalyticManifold.Inverse
import Mathlib.branch.GeoffreyIrving.Ray.AnalyticManifold.Nontrivial
import Mathlib.branch.GeoffreyIrving.Ray.AnalyticManifold.OpenMapping

/-!
## Nonzero derivative holomorphic functions are locally injective

This is a straightforward consequence of the inverse function theorem.  We also prove
parameterized versions, where `f : ‚ÑÇ ‚Üí S ‚Üí T`.
-/

open Classical
open Filter (eventually_of_forall Tendsto)
open Function (uncurry)
open OneDimension
open Set
open scoped Topology
noncomputable section

variable {S : Type} [TopologicalSpace S] [ChartedSpace ‚ÑÇ S] [cms : AnalyticManifold I S]
variable {T : Type} [TopologicalSpace T] [ChartedSpace ‚ÑÇ T] [cmt : AnalyticManifold I T]

/-- Nonzero derivative holomorphic functions are locally injective -/
theorem HolomorphicAt.local_inj {f : S ‚Üí T} {z : S}
    (fa : HolomorphicAt I I f z) (nc : mfderiv I I f z ‚â† 0) :
    ‚àÄ·∂† p : S √ó S in ùìù (z, z), f p.1 = f p.2 ‚Üí p.1 = p.2 := by
  rcases complex_inverse_fun' fa nc with ‚ü®g, ga, gf, fg‚ü©
  have n : NontrivialHolomorphicAt g (f z) := by
    rw [‚Üê gf.self] at fa
    refine' (NontrivialHolomorphicAt.anti _ fa ga).2
    exact (nontrivialHolomorphicAt_id _).congr (Filter.EventuallyEq.symm fg)
  have o := n.nhds_eq_map_nhds; rw [gf.self] at o
  simp only [nhds_prod_eq, o, Filter.prod_map_map_eq, Filter.eventually_map]
  refine' (fg.prod_mk fg).mp (eventually_of_forall _); intro ‚ü®x, y‚ü© ‚ü®ex, ey‚ü© h
  simp only at ex ey; simp only [ex, ey] at h; simp only [h]

/-- Nonzero derivative holomorphic functions are locally injective, parameterized version.
    Specifically, we show local injectivity of `(c,z) ‚Ü¶ (c, f c z)`. -/
theorem HolomorphicAt.local_inj'' {f : ‚ÑÇ ‚Üí S ‚Üí T} {c : ‚ÑÇ} {z : S}
    (fa : HolomorphicAt II I (uncurry f) (c, z)) (nc : mfderiv I I (f c) z ‚â† 0) :
    ‚àÄ·∂† p : (‚ÑÇ √ó S) √ó ‚ÑÇ √ó S in ùìù ((c, z), (c, z)),
      p.1.1 = p.2.1 ‚Üí f p.1.1 p.1.2 = f p.2.1 p.2.2 ‚Üí p.1 = p.2 := by
  rcases complex_inverse_fun fa nc with ‚ü®g, ga, gf, fg‚ü©
  have n : NontrivialHolomorphicAt (g c) (f c z) := by
    have e : (c, z) = (c, g c (f c z)) := by rw [gf.self]
    rw [e] at fa
    refine' (NontrivialHolomorphicAt.anti _ fa.in2 ga.in2).2
    refine' (nontrivialHolomorphicAt_id _).congr _
    refine' ((continuousAt_const.prod continuousAt_id).eventually fg).mp (eventually_of_forall _)
    exact fun _ e ‚Ü¶ e.symm
  have o := n.nhds_eq_map_nhds_param ga; rw [gf.self] at o; simp only at o
  rw [nhds_prod_eq, o]; simp only [Filter.prod_map_map_eq, Filter.eventually_map]
  refine' (fg.prod_mk fg).mp (eventually_of_forall _); intro ‚ü®x, y‚ü© ‚ü®ex, ey‚ü© h1 h2
  simp only at h1; simp only [h1] at ex ey h2 ‚ä¢; simp only [ex, ey] at h2; simp only [h2]

/-- Nonzero derivative holomorphic functions are locally injective, parameterized version.
    Specifically, we show local injectivity of `(c,z) ‚Ü¶ (c, f c z)`. -/
theorem HolomorphicAt.local_inj' {f : ‚ÑÇ ‚Üí S ‚Üí T} {c : ‚ÑÇ} {z : S}
    (fa : HolomorphicAt II I (uncurry f) (c, z)) (nc : mfderiv I I (f c) z ‚â† 0) :
    ‚àÄ·∂† p : ‚ÑÇ √ó S √ó S in ùìù (c, z, z), f p.1 p.2.1 = f p.1 p.2.2 ‚Üí p.2.1 = p.2.2 := by
  set g : ‚ÑÇ √ó S √ó S ‚Üí (‚ÑÇ √ó S) √ó ‚ÑÇ √ó S := fun p ‚Ü¶ ((p.1, p.2.1), (p.1, p.2.2))
  have t : Tendsto g (ùìù (c, z, z)) (ùìù ((c, z), (c, z))) := by
    apply Continuous.continuousAt; apply Continuous.prod
    ¬∑ exact continuous_fst.prod (continuous_fst.comp continuous_snd)
    ¬∑ exact continuous_fst.prod (continuous_snd.comp continuous_snd)
  refine' (t.eventually (fa.local_inj'' nc)).mp (eventually_of_forall _)
  intro ‚ü®e, x, y‚ü© inj fe; exact (Prod.ext_iff.mp (inj rfl fe)).2
